(*
 *	ell2 - a redesign of ell
 *
 *	$RCSfile: CodeC.ESTRA,v $
 *	
 *	purpose:	generation of C code
 *
 *	$Author: grosch $
 *	$Date: 1992/10/15 18:26:55 $
 *)


.. ^ Spaces;
.. $ WriteNl (file);
.. + INC (SpaCo%);
.. - DEC (SpaCo%);
.. % WriteS (file, '%');
.. n WriteI (file, %, 0);
.. i WriteIdent (file, %);
.. s WriteS (file, %);
.. c WriteI (file, Code (%), 0);
.. I WriteIndex (file, %);
.. R WriteRecIndex (file, %);


TRANSFORMATION	CodeC

GLOBAL	{
FROM ArgCheck	IMPORT	DEF, IMP, LINE, SourceFileName;
FROM Checks	IMPORT	CheckWriteOpen, CheckOpenOutput;
FROM Codes	IMPORT	cNoIndex, vMaxRecSetIndex, vMaxSetIndex,
			Code, MaxCode, WriteIndex, WriteRecIndex,
			Term, GetSet, GetRecSet;
FROM DynArray	IMPORT	MakeArray, ReleaseArray;
FROM Errors	IMPORT	ERROR;
FROM IO		IMPORT	tFile, WriteS, WriteC, WriteI, WriteNl,
			WriteOpen, WriteClose;
FROM Idents	IMPORT	tIdent, WriteIdent, MaxIdent, GetString;
FROM Sets	IMPORT	tSet, MakeSet, ReleaseSet, Extract, IsEmpty, Assign,
			Union, Include, Exclude, IsElement, Minimum, Maximum;
FROM StringMem	IMPORT	WriteString;
FROM SYSTEM	IMPORT	TSIZE, ADR;
FROM System	IMPORT	OpenOutput, Write, Close;
FROM Scanner	IMPORT	NoIdent, Epsilon;
FROM Types	IMPORT	IsTerm, IsNonterm, Terminals;
FROM Strings	IMPORT	tString, Char, Length, ArrayToString, Concatenate, Append,
			StringToArray;
FROM Tree	IMPORT	ScannerName, ParserName;

IMPORT Idents;

CONST
  cBitsPerChar		= 8;
  cBitsPerLong		= 32;
  cDefinition		= '.h';
  cImplementation	= '.c';

VAR
  file		: tFile;
  SpaCo, Label	: INTEGER;
  vExport	: BOOLEAN;
  InDefault	: BOOLEAN;
  lSections	: Tree.tTree;
  PathS, String	: tString;
  PathA		: ARRAY [0..128] OF CHAR;


PROCEDURE Prefix;
  BEGIN
    IF ScannerName # Idents.NoIdent THEN
.	{i ScannerName}_\
    END;
  END Prefix;

PROCEDURE Spaces;
  VAR i: INTEGER;
  BEGIN FOR i := 1 TO SpaCo DO WriteS (file, '  '); END; END Spaces;

PROCEDURE CodeIsElement (index: INTEGER; set: tSet);
  VAR
    todo: tSet;
    id: tIdent;
    sl: INTEGER;
  BEGIN
    IF index = cNoIndex THEN
      MakeSet (todo, MaxIdent ());
      Assign (todo, set);
      Exclude (todo, Epsilon);
      IF NOT IsEmpty (todo) THEN
	LOOP
	  id := Extract (todo);
	  WriteS (file, '(xxToken == ');
	  WriteI (file, Code (id), 0);
	  WriteS (file, ' /* ');
	  WriteIdent (file, id);
	  WriteS (file, ' */)');
	  IF IsEmpty (todo) THEN EXIT END;
	  WriteS (file, ' || ');
	END;
      END;
      ReleaseSet (todo);
    ELSE
      sl := index DIV cBitsPerChar;
      WriteS (file, '(xxVerticalSet [xxToken');
      IF sl # 0 THEN
	WriteS (file, ' + ');
	WriteI (file, sl * (MaxCode () + 1), 0);
      END;
      WriteS (file, '] & 1 << ');
      WriteI (file, index MOD cBitsPerChar, 0);
      WriteS (file, ' /* ');
      WriteIndex (file, index);
      WriteS (file, ' */)');
    END;
  END CodeIsElement;

PROCEDURE CodeRecovery (recover, expect: INTEGER);
  BEGIN
    IF expect = cNoIndex THEN
.     xxUnexpected ({n recover}, xxGlobalRecoverySet); /* {R recover} */
    ELSE
.     xxExpected ({n expect}, {n recover}, xxGlobalRecoverySet); /* {R expect} / {R recover} */
    END;
  END CodeRecovery;

PROCEDURE WriteLabels (set: tSet);
  VAR
    todo: tSet;
    id: tIdent;
  BEGIN
    MakeSet (todo, MaxIdent ());
    Assign (todo, set);
    WHILE NOT IsEmpty (todo) DO
      id := Extract (todo);
.     case \
      WriteI (file, Code (id), 0);
      WriteS (file, ' /* ');
      WriteIdent (file, id);
      WriteS (file, ' */ :; ');
      WriteNl (file);
    END;
    ReleaseSet (todo);
  END WriteLabels;

VAR
  NumberArray: POINTER TO ARRAY [0..1000] OF INTEGER;
  NumberArraySize: LONGINT;
  MaxNumberArray: POINTER TO ARRAY [0..1000] OF INTEGER;
  MaxNumberArraySize: LONGINT;

PROCEDURE ClearArray (VAR a: ARRAY OF INTEGER);
  VAR id: tIdent;
  BEGIN
    FOR id := 0 TO MaxIdent () DO a [id] := 0; END;
  END ClearArray;

PROCEDURE WriteAttributes;
  VAR
    id: tIdent;
    i, max: INTEGER;
  BEGIN
    FOR id := 0 TO MaxIdent () DO
      max := MaxNumberArray^ [id];
      IF max > 0 THEN
	IF IsTerm (id) THEN
.	\  \
	  Prefix;
.	   tScanAttribute \
	ELSE
.	\  {i ParserName}_tParsAttribute \
	END;
	FOR i := 1 TO max - 1 DO
.	  {i id}{n i}, \
	END;
.	{i id}{n max};
      END;
    END;
  END WriteAttributes;

VAR Hex: ARRAY [0..15] OF CHAR;

PROCEDURE WriteRecSets;
  VAR
    value: INTEGER;
    set: tSet;
    no, bit, bitvalue: INTEGER;
    index: INTEGER;
  BEGIN
    MakeSet (set, MaxCode ());
    FOR index := 0 TO vMaxRecSetIndex DO
      GetRecSet (index, set);
      WriteS (file, '  {');
      FOR no := 0 TO MaxCode () DIV cBitsPerLong DO
	WriteS (file, ' 0x');
        FOR bit := 0 TO cBitsPerLong - 1 DO
	  IF bit MOD 4 = 0 THEN
	    value := 0;
	    bitvalue := 8;
	  END;
	  IF IsElement (no * cBitsPerLong + bit, set) THEN
	    INC (value, bitvalue);
	  END;
	  bitvalue := bitvalue DIV 2;
	  IF bit MOD 4 = 3 THEN
	    WriteC (file, Hex [value]);
	  END;
	END;
	WriteS (file, 'L,');
	IF no MOD 4 = 3 THEN WriteNl (file); END;
      END;
      WriteS (file, ' },');
      WriteNl (file);
    END;
    ReleaseSet (set);
  END WriteRecSets;

PROCEDURE WriteVerticalSet (no: INTEGER);
  VAR
    sets: ARRAY [0..cBitsPerChar-1] OF tSet;
    code, bit: INTEGER;
    index: INTEGER;
    value, bitvalue: INTEGER;
  BEGIN
    FOR bit := 0 TO cBitsPerChar - 1 DO
      MakeSet (sets [bit], MaxCode ());
      index := no * cBitsPerChar + bit;
      GetSet (index, sets [bit]);
    END;
    FOR code := 0 TO MaxCode () DO
      IF code MOD 8 = 0 THEN WriteNl (file); END;
      WriteS (file, ' 0x');
      FOR bit := cBitsPerChar - 1 TO 0 BY -1 DO 
	IF bit MOD 4 = 3 THEN
	  value := 0;
	  bitvalue := 8;
	END;
	IF IsElement (code, sets [bit]) THEN
	  INC (value, bitvalue);
	END;
	IF bit MOD 4 = 0 THEN
	  WriteC (file, Hex [value]);
	END;
	bitvalue := bitvalue DIV 2;
      END;
      WriteS (file, ',');
    END;
    WriteNl (file);
    FOR bit := 0 TO cBitsPerChar - 1 DO
      ReleaseSet (sets [bit]);
    END;
  END WriteVerticalSet;
}

BEGIN	{
  vExport := FALSE;
  SpaCo := 0;
  Label := 0;
  Hex [0] := '0';
  Hex [1] := '1';
  Hex [2] := '2';
  Hex [3] := '3';
  Hex [4] := '4';
  Hex [5] := '5';
  Hex [6] := '6';
  Hex [7] := '7';
  Hex [8] := '8';
  Hex [9] := '9';
  Hex [10] := 'A';
  Hex [11] := 'B';
  Hex [12] := 'C';
  Hex [13] := 'D';
  Hex [14] := 'E';
  Hex [15] := 'F';
}

GRAMMAR Tree

grammar		= 
| Grammar	(sections, tokens, rules)

sections	=
| Sections0	()
| Sections	(section, sections)

section		=
| Export	(codes)
| Global	(codes)
| Local		(codes)
| Begin		(codes)
| Close		(codes)

codes		=
| Codes0	()
| Codes		(code, codes)

code		=
| Code		()

tokens		=
| Tokens0	()
| Tokens	(token, tokens)

token		=
| Token		(id, number)

id		=
| Id		()

number		=
| Number	()

rules		=
| Rules0	()
| Rules		(rule, rules)

rule		=
| Rule		(id, codes, expr)

expr		=
| Option	(expr)
| Times		(expr)
| Plus		(expr)
| List		(body: expr, sep: expr)
| Action	(codes)
| Leaf		(id)

expr		->
alternative	=
| Alternative0	()
| Alternative	(expr, alternative)

expr		->
sequence	=
| Sequence0	()
| Sequence	(expr, sequence)


FUNCTION	CODE			/ grammar /

  grammar
		{
		IF DEF THEN CodeDef (grammar); END;
		IF IMP THEN CodeImp (grammar); END;
		}


/*
 *	generate definition of parser
 */

FUNCTION	CodeDef			/ grammar /

  Grammar	(sections, tokens, rules)
		{
		GetString	(ParserName, PathS);
		ArrayToString	(cDefinition, String);
		Concatenate	(PathS, String);
		Append		(PathS, 0C);
		StringToArray	(PathS, PathA);
		file := WriteOpen (PathA);
		CheckWriteOpen (file, PathA);

		IF ScannerName # Idents.NoIdent THEN
.		   # include "{i ScannerName}.h"
		ELSE
.		   # include "Scanner.h"
		END;
.
.		# if defined __STDC__ | defined __cplusplus
.		# define ARGS(parameters)	parameters
.		# else
.		# define ARGS(parameters)	()
.		# endif
.
		CodeExport (sections);
		IF NOT vExport THEN	(* Default EXPORT action *)
.		typedef	\
		Prefix;
.		   tScanAttribute {i ParserName}_tParsAttribute;
		END;
.
.		extern	{i ParserName}_tParsAttribute {i ParserName}_ParsAttribute;
.		extern	char *	{i ParserName}_TokenName [];
.
.		extern	int	{i ParserName}		();
.		extern	void	Close{i ParserName}	();
		WriteClose (file);
		}


/*
 *	generate implementation of parser
 */

FUNCTION	CodeImp			/ grammar /

  Grammar	(sections, tokens, Rules (Rule (Id (), codes, expr), rules))
		DECLARE	{ VAR
		  i	: INTEGER;
		  j	: CARDINAL;
		  set	: tSet;
		  id	: tIdent;
		  s	: tString;
		  c	: CHAR;
		}
		{
		GetString	(ParserName, PathS);
		ArrayToString	(cImplementation, String);
		Concatenate	(PathS, String);
		Append		(PathS, 0C);
		StringToArray	(PathS, PathA);
		file := WriteOpen (PathA);
		CheckWriteOpen (file, PathA);

.		# include	"{i ParserName}.h"
.		# include	"Errors.h"
.
.		# define	xxbool	char
.		# define	xxtrue	1
.		# define	xxfalse	0
.		# define	xxEof	0
.
		CodeGlobal (sections);
		lSections := sections;

.		typedef struct xxsUnion \{
.	       \	  struct xxsUnion *	xxGlobalRecoverySet;
.	       \	  unsigned short	xxLocalRecoverySet;
.	       \	\} xxtUnion, * xxtUnionPtr;
.		typedef	long	xxtSet [{n MaxCode () DIV cBitsPerLong + 1}];
.
.		{i ParserName}_tParsAttribute	{i ParserName}_ParsAttribute;
.
.		static short	xxToken;
.		static int	xxErrorCount;
.		static xxbool	xxIsInitialized = xxfalse, xxIsRepairMode = xxfalse;
		IF vMaxRecSetIndex = cNoIndex THEN
.		static long	xxHorizontalSet [] = \{ 0 \};
		ELSE
.		static long	xxHorizontalSet [] [{n MaxCode () DIV cBitsPerLong + 1}] = \{
		WriteRecSets;
.		\};
		END;
.		static unsigned char xxVerticalSet [] = \{\
		FOR i := 0 TO vMaxSetIndex DIV cBitsPerChar DO
		  WriteVerticalSet (i);
		END;
.		\};

.		char *		{i ParserName}_TokenName [] = \{
		FOR i := 0 TO MaxCode () DO
		  id := Term (i);
		  IF id # NoIdent THEN
		    GetString (id, s);
.		    "\
		    c := Char (s, 1);
		    IF (c = '"') OR (c = "'") THEN
		      FOR j := 2 TO Length (s) - 1 DO
		        c := Char (s, j);
		        IF (c = '\') OR (c = '"') THEN WriteC (file, '\'); END;
		        WriteC (file, c);
		      END;
		    ELSE
		      FOR j := 1 TO Length (s) DO
		        c := Char (s, j);
		        IF (c = '\') OR (c = '"') THEN WriteC (file, '\'); END;
		        WriteC (file, c);
		      END;
		    END;
.		    ",
		  ELSE
.		    0,
		  END;
		END;
.		\};
.
		CodeForward (Rules);
.
.		static void Begin{i ParserName} ()
.		\{
.+
.	          if (xxIsInitialized) return;
		CodeBegin (sections);
.-
.		\}
.
.		void Close{i ParserName} ()
.		\{
		CodeClose (sections);
.		\}
.
.		static xxbool xxIsElement
.		# if defined __STDC__ | defined __cplusplus
.		(xxtSet xySet, short xxElement)
.		# else
.		(xySet, xxElement) xxtSet xySet; short xxElement;
.		# endif
.		\{ return xySet [xxElement / {n cBitsPerLong}] << xxElement % {n cBitsPerLong} < 0; \}
.
.		static void xxSkipTokens
.		# if defined __STDC__ | defined __cplusplus
.		(unsigned short xxLocalRecoverySet, xxtUnionPtr xxGlobalRecoverySet)
.		# else
.		(xxLocalRecoverySet, xxGlobalRecoverySet)
.		unsigned short	xxLocalRecoverySet;
.		xxtUnionPtr	xxGlobalRecoverySet;
.		# endif
.		\{
.	       \  xxbool xxTokensSkipped = xxfalse;
.	       \  xxtSet xxRecoverySet;
		FOR i := 0 TO MaxCode () DIV cBitsPerLong DO
.	       \  xxRecoverySet [{n i}] = xxHorizontalSet [xxLocalRecoverySet] [{n i}];
		END;
.	       \  xxRecoverySet [0] |= (unsigned long) 0x80000000L; /* INCL (xxRecoverySet [0], xxEof); */
.	       \  while (xxGlobalRecoverySet != (xxtUnionPtr) 0L) \{
		FOR i := 0 TO MaxCode () DIV cBitsPerLong DO
.	       \    xxRecoverySet [{n i}] |= xxHorizontalSet [xxGlobalRecoverySet->xxLocalRecoverySet] [{n i}];
		END;
.	       \    xxGlobalRecoverySet = xxGlobalRecoverySet->xxGlobalRecoverySet;
.	       \  \}
.	       \  while (! xxIsElement (xxRecoverySet, xxToken)) \{
.	       \    xxToken = \
		Prefix;
.		     GetToken (); xxTokensSkipped = xxtrue;
.	       \  \}
.	       \  if (xxTokensSkipped) ErrorMessage (xxRestartPoint, xxInformation, \
		Prefix;
.		   Attribute.Position);
.	       \  xxIsRepairMode = xxtrue;
.		\}
.
.		static void xxUnexpected
.		# if defined __STDC__ | defined __cplusplus
.		(unsigned short xxLocalRecoverySet, xxtUnionPtr xxGlobalRecoverySet)
.		# else
.		(xxLocalRecoverySet, xxGlobalRecoverySet)
.		unsigned short	xxLocalRecoverySet;
.		xxtUnionPtr	xxGlobalRecoverySet;
.		# endif
.		\{
.	       \  if (! xxIsRepairMode) \{
.	       \    xxErrorCount ++;
.	       \    ErrorMessage (xxSyntaxError, xxError, \
		Prefix;
.		     Attribute.Position);
.	       \    xxSkipTokens (xxLocalRecoverySet, xxGlobalRecoverySet);
.	       \  \}
.		\}
.
.		static void xxExpected
.		# if defined __STDC__ | defined __cplusplus
.		(unsigned short xxExpectedSet, unsigned short xxLocalRecoverySet, xxtUnionPtr xxGlobalRecoverySet)
.		# else
.		(xxExpectedSet, xxLocalRecoverySet, xxGlobalRecoverySet)
.		unsigned short	xxExpectedSet, xxLocalRecoverySet;
.		xxtUnionPtr	xxGlobalRecoverySet;
.		# endif
.		\{
.	       \  if (! xxIsRepairMode) \{
.	       \    short yxToken;
.	       \    int xxLength = 0;
.	       \    char xxContinueString [256];
.	       \    xxErrorCount ++;
.	       \    ErrorMessage (xxSyntaxError, xxError, \
		Prefix;
.		     Attribute.Position);
.. % WriteS (file, "%");
.	       \    xxContinueString [0] = '\\0';
.. % WriteS (file, '%');
.	       \    for (yxToken = 0; yxToken <= {n MaxCode ()}; yxToken ++)
.	       \      if (xxIsElement (xxHorizontalSet [xxExpectedSet], yxToken)) \{
.	       \	char * xxTokenString = {i ParserName}_TokenName [yxToken];
.	       \	if ((xxLength += strlen (xxTokenString) + 1) >= 256) break;
.	       \	(void) strcat (xxContinueString, xxTokenString);
.	       \	(void) strcat (xxContinueString, " ");
.	       \      \}
.	       \    ErrorMessageI (xxExpectedTokens, xxInformation, \
		Prefix;
.		     Attribute.Position, xxString, xxContinueString);
.	       \    xxSkipTokens (xxLocalRecoverySet, xxGlobalRecoverySet);
.	       \  \}
.		\}
.
.		static void xxRecoveryLiteral
.		# if defined __STDC__ | defined __cplusplus
.		(unsigned short xxExpected, unsigned short xxLocalRecoverySet, xxtUnionPtr xxGlobalRecoverySet)
.		# else
.		(xxExpected, xxLocalRecoverySet, xxGlobalRecoverySet)
.		unsigned short	xxExpected, xxLocalRecoverySet;
.		xxtUnionPtr	xxGlobalRecoverySet;
.		# endif
.		\{
.	       \  if (! xxIsRepairMode) \{
.	       \    xxErrorCount ++;
.	       \    ErrorMessage (xxSyntaxError, xxError, \
		Prefix;
.		     Attribute.Position);
.	       \    ErrorMessageI (xxExpectedTokens, xxInformation, \
		Prefix;
.		     Attribute.Position, xxString, {i ParserName}_TokenName [xxExpected]);
.	       \    xxSkipTokens (xxLocalRecoverySet, xxGlobalRecoverySet);
.	       \  \}
.	       \  if (xxToken != xxExpected) \{
.	       \    ErrorMessageI (xxTokenInserted, xxRepair, \
		Prefix;
.		     Attribute.Position, xxString, {i ParserName}_TokenName [xxExpected]);
.	       \  \} else \{
.	       \    if (xxToken != xxEof) xxToken = \
		Prefix;
.		     GetToken ();
.	       \    xxIsRepairMode = xxfalse;
.	       \  \}
.		\}
.
.		static void xxRecoveryTerminal
.		# if defined __STDC__ | defined __cplusplus
.		(unsigned short xxExpected, unsigned short xxLocalRecoverySet,
.		xxtUnionPtr xxGlobalRecoverySet, \
		Prefix;
.		   tScanAttribute * xxRepairAttribute)
.		# else
.		(xxExpected, xxLocalRecoverySet, xxGlobalRecoverySet, xxRepairAttribute)
.		unsigned short	xxExpected, xxLocalRecoverySet;
.		xxtUnionPtr	xxGlobalRecoverySet;
		Prefix;
.		   tScanAttribute	* xxRepairAttribute;
.		# endif
.		\{
.	       \  if (! xxIsRepairMode) \{
.	       \    xxErrorCount ++;
.	       \    ErrorMessage (xxSyntaxError, xxError, \
		Prefix;
.		     Attribute.Position);
.	       \    ErrorMessageI (xxExpectedTokens, xxInformation, \
		Prefix;
.		     Attribute.Position, xxString, {i ParserName}_TokenName [xxExpected]);
.	       \    xxSkipTokens (xxLocalRecoverySet, xxGlobalRecoverySet);
.	       \  \}
.	       \  if (xxToken != xxExpected) \{
.	       \    ErrorMessageI (xxTokenInserted, xxRepair, \
		Prefix;
.		     Attribute.Position, xxString, {i ParserName}_TokenName [xxExpected]);
.	       \    \
		Prefix;
.		   ErrorAttribute ((int) xxExpected, xxRepairAttribute);
.	       \  \} else \{
.	       \    * xxRepairAttribute = \
		Prefix;
.		     Attribute;
.	       \    if (xxToken != xxEof) xxToken = \
		Prefix;
.		     GetToken ();
.	       \    xxIsRepairMode = xxfalse;
.	       \  \}
.		\}
.
.		int {i ParserName} ()
.		\{
.	       \  xxErrorCount = 0;
.	       \  Begin{i ParserName} ();
.	       \  xxToken = \
		Prefix;
.		   GetToken ();
.	       \  yy{i Id.ident} (& {i ParserName}_ParsAttribute, (xxtUnionPtr) 0L);
.	       \  if (xxToken != xxEof) xxRecoveryLiteral (xxEof, 0, (xxtUnionPtr) 0L);
.	       \  return xxErrorCount;
.		\}
.
		NumberArraySize := MaxIdent () + 1;
		MaxNumberArraySize := MaxIdent () + 1;
		MakeArray (NumberArray, NumberArraySize, TSIZE (INTEGER));
		MakeArray (MaxNumberArray, MaxNumberArraySize, TSIZE (INTEGER));
		CodeRules (Rules);
		ReleaseArray (NumberArray, NumberArraySize, TSIZE (INTEGER));
		ReleaseArray (MaxNumberArray, MaxNumberArraySize, TSIZE (INTEGER));

		WriteClose (file);
		}

  Grammar	(sections, tokens, Rules0 ())
		{
		ERROR ('CodeM2.ESTRA: no rules');
		}


/*
 *	generate forward declarations
 */

FUNCTION	CodeForward		/rules/
  
  Rules0	() {}

  Rules		(Rule (Id (), codes, expr), rules)
		{
.		static void yy{i Id.ident} ARGS(({i ParserName}_tParsAttribute * {i Id.ident}0, xxtUnionPtr xxGlobalRecoverySet));
		CodeForward (rules);
		}

/*
 *	generate recursive descent procedure
 */

FUNCTION	CodeRules		/rules, expr/

  Rules0	() {}

  Rules		(Rule (Id (), codes, expr), rules)
		{
		ClearArray (MaxNumberArray^);
		SetNumbers (expr, TRUE);

.		static void yy{i Id.ident}
.		# if defined __STDC__ | defined __cplusplus
.		({i ParserName}_tParsAttribute * {i Id.ident}0, xxtUnionPtr xxGlobalRecoverySet)
.		# else
.		({i Id.ident}0, xxGlobalRecoverySet)
.		{i ParserName}_tParsAttribute	* {i Id.ident}0;
.		xxtUnionPtr	xxGlobalRecoverySet;
.		# endif
.		\{
.+
		  WriteAttributes;
		  IF Rule.iscalling THEN
.		    xxtUnion xxUnion;
		  END;
		  CodeLocal (lSections);
		  CodeCodes (codes);
		  IF Rule.iscalling THEN
.		    xxUnion.xxGlobalRecoverySet = xxGlobalRecoverySet;
		  END;
		  CodeRules (expr);
.-
.		\}
.
		CodeRules (rules);
		}

  Option	(expr)
		{
.		for (;;) \{
.+
.		  if (\
		    CodeIsElement (Option.index, expr.first);
.		    ) \{
.+
		    InDefault := FALSE;
		    CodeRules (expr);
.		    break;
.-
.		  \} else if (\
		    CodeIsElement (Option.followindex, Option.follow);
.		  \ || xxIsRepairMode) break;
		  CodeRecovery (Option.recindex, Option.expindex);
.-
.		\}
		}

  Times		(expr)
		{
.		for (;;) \{
.+
.		  if (\
		      CodeIsElement (Times.index, expr.first);
.		      ) \{
.+
		    InDefault := FALSE;
		    CodeRules (expr);
.-
.		  \} else if (\
		    CodeIsElement (Times.followindex, Times.follow);
.		  \ || xxIsRepairMode) break;
.		  else
.+
		    CodeRecovery (Times.recindex, Times.expindex);
.-
.-
.		\}
		}

  Plus		(expr)
		{
.		for (;;) \{
.+
		  CodeRules (expr);
.		  if (! (\
		       CodeIsElement (Plus.index, expr.first);
.		       )) \{
.+
.		    if (\
		      CodeIsElement (Plus.followindex, Plus.follow);
.		      ) break;
		    CodeRecovery (Plus.recindex, Plus.expindex);
.		    if (! (\
		      CodeIsElement (Plus.index, Plus.first);
.		      )) break;
.-
.		  \}
.-
.		\}
		}

  List		(body: expr, sep: expr)
		{
.		for (;;) \{
.+
		  CodeRules (body);
.		  if (! (\
		    CodeIsElement (sep.index, sep.first);
.			 )) \{
.+
.		    if (\
		      CodeIsElement (List.followindex, List.follow);
.		      ) break;
		    CodeRecovery (List.recindex, List.expindex);
.		    if (! (\
		      CodeIsElement (sep.index, sep.first);
.		    \ || \
		      CodeIsElement (List.index, List.first);
.		      )) break;
.-
.		  \}
		  CodeRules (sep);
.-
.		\}
		}

  Action	(Codes0 ()) {}

  Action	(codes) { CodeCodes (codes); }

  Leaf		(Id ())
		CONDITION	{ IsTerm (Id.ident) & Leaf.sure & NOT Id.isstring }
		{
		IF InDefault THEN
.		  if (xxToken != {c Id.ident} /* {i Id.ident} */)
.	         \  xxRecoveryTerminal ({c Id.ident}, {n Leaf.recindex}, xxGlobalRecoverySet, & {i Id.ident}{n Leaf.number}); /* {R Leaf.recindex} */
.		  else \{
.	         \  {i Id.ident}{n Leaf.number} = \
		  Prefix;
.		     Attribute;
.	         \  xxToken = \
		Prefix;
.		     GetToken (); xxIsRepairMode = xxfalse;
.		  \}
		  InDefault := FALSE;
		ELSE
.		  {i Id.ident}{n Leaf.number} = \
		Prefix;
.		   Attribute;
.		  xxToken = \
		Prefix;
.		   GetToken (); xxIsRepairMode = xxfalse;
		END;
		}

  Leaf		(Id ())
		CONDITION	{ IsTerm (Id.ident) & Leaf.sure & Id.isstring }
		{
		IF InDefault THEN
.		  if (xxToken != {c Id.ident} /* {i Id.ident} */)
.	         \  xxRecoveryLiteral ({c Id.ident}, {n Leaf.recindex}, xxGlobalRecoverySet); /* {R Leaf.recindex} */
.		  else \{
.	         \  xxToken = \
		Prefix;
.		     GetToken (); xxIsRepairMode = xxfalse;
.		  \}
		  InDefault := FALSE;
		ELSE
.		  xxToken = \
		Prefix;
.		   GetToken (); xxIsRepairMode = xxfalse;
		END;
		}

  Leaf		(Id ())
		CONDITION	{ IsTerm (Id.ident) & NOT Leaf.sure & NOT Id.isstring }
		{
.		if (xxToken != {c Id.ident} /* {i Id.ident} */)
.	       \  xxRecoveryTerminal ({c Id.ident}, {n Leaf.recindex}, xxGlobalRecoverySet, & {i Id.ident}{n Leaf.number}); /* {R Leaf.recindex} */
.		else \{
.	       \  {i Id.ident}{n Leaf.number} = \
		Prefix;
.		   Attribute;
.	       \  xxToken = \
		Prefix;
.		   GetToken (); xxIsRepairMode = xxfalse;
.		\}
		}

  Leaf		(Id ())
		CONDITION	{ IsTerm (Id.ident) & NOT Leaf.sure & Id.isstring }
		{
.		if (xxToken != {c Id.ident} /* {i Id.ident} */)
.	       \  xxRecoveryLiteral ({c Id.ident}, {n Leaf.recindex}, xxGlobalRecoverySet); /* {R Leaf.recindex} */
.		else \{
.	       \  xxToken = \
		Prefix;
.		   GetToken (); xxIsRepairMode = xxfalse;
.		\}
		}

  Leaf		(Id ())
		CONDITION	{ IsNonterm (Id.ident) }
		{
.		xxUnion.xxLocalRecoverySet = {n Leaf.recindex}; yy{i Id.ident} (& {i Id.ident}{n Leaf.number}, & xxUnion); /* {R Leaf.recindex} */
		}

  Sequence0	() {}

  Sequence	(expr, sequence)
		{
		CodeRules (expr);
		CodeRules (sequence);
		}

  alternative	CONDITION	{ alternative.case }
		DECLARE { VAR label: INTEGER; labels: tSet; }
		{
		MakeSet (labels, MaxIdent ());
.		for (;;) \{
.+
.		  switch (xxToken) \{
		  INC (Label);
		  label := Label;
		  CodeAltCase (alternative, label, labels);
.		  \}
.-
.		\}
.		xx{n label}:;
		ReleaseSet (labels);
		}

  alternative	CONDITION	{ NOT alternative.case }
		{
		CodeAltIf (alternative, FALSE);
		}


FUNCTION	CodeAltCase	label: INTEGER; labels: tSet ->	/ alternative /

  Alternative0	()
		DECLARE { VAR l: CARDINAL; s: tSet; }
		{
		MakeSet (s, MaxCode ());
		FOR l := Minimum (labels) TO Maximum (labels) DO
		  IF IsElement (l, labels) THEN
		    Include (s, Code (l));
		  END;
		END;
		FOR l := Minimum (s) TO Maximum (s) DO
		  IF NOT IsElement (l, s) THEN
.		    case {n l}:;
		  END;
		END;
.		default:
.+
.		  if (xxIsRepairMode) \{
.+
		    InDefault := TRUE;
		    CodeRules (Alternative0.default);
		    InDefault := FALSE;
.		    goto xx{n label};
.-
.		  \}
		  CodeRecovery (Alternative0.recindex, Alternative0.expindex);
.-
		}

  Alternative	(expr, alternative)
		{
		WriteLabels (expr.fifo);
.+
		CodeRules (expr);
.		goto xx{n label};
.-
		Union (labels, expr.fifo);
		CodeAltCase (alternative, label, labels);
		}


FUNCTION	CodeAltIf	started: BOOLEAN ->	/ alternative /

  Alternative0	()
		{
.		\} else \{
.+
		  CodeRecovery (Alternative0.recindex, Alternative0.expindex);
		  InDefault := TRUE;
		  CodeRules (Alternative0.default);
		  InDefault := FALSE;
.-
.		\}
		}

  Alternative	(expr, Alternative0 ())
		{
		IF NOT started THEN
.		  if (\
	            CodeIsElement (Alternative.index, expr.first);
.	            ) \{
		ELSE
.		  \} else \{
		END;
.+
		CodeRules (expr);
.-
.		\}
		}

  Alternative	(expr, alternative)
		{
		IF NOT started THEN
.		  if (\
		ELSE
.		  \} else if (\
		END;
	        CodeIsElement (Alternative.index, expr.fifo);
.	        ) \{
.+
		CodeRules (expr);
.-
		CodeAltIf (alternative, TRUE);
		}


/*
 *	set numbers for attribute access
 */

FUNCTION	SetNumbers	start: BOOLEAN	->	/expr/

Option		(expr)
		{
		IF start THEN ClearArray (NumberArray^); END;
		SetNumbers (expr, FALSE);
		}

Times		(expr)
		{
		IF start THEN ClearArray (NumberArray^); END;
		SetNumbers (expr, FALSE);
		}

Plus		(expr)
		{
		IF start THEN ClearArray (NumberArray^); END;
		SetNumbers (expr, FALSE);
		}

List		(body: expr, sep: expr)
		{
		IF start THEN ClearArray (NumberArray^); END;
		SetNumbers (body, FALSE);
		SetNumbers (sep, FALSE);
		}

Action		(codes) {}

Sequence0	() {}

Sequence	(expr, sequence)
		{
		IF start THEN ClearArray (NumberArray^); END;
		SetNumbers (expr, FALSE);
		SetNumbers (sequence, FALSE);
		}

Alternative0	() { }

Alternative	(expr, alternative)
		{
		IF start THEN ClearArray (NumberArray^); END;
		SetNumbers (expr, FALSE);
		SetNumbers (alternative, start);
		}

Leaf		(Id ())
		CONDITION	{ Id.isstring }
		{
		Leaf.number := 0;
		}

Leaf		(Id ())
		CONDITION	{ NOT Id.isstring }
		{
		IF start THEN ClearArray (NumberArray^); END;
		Leaf.number := NumberArray^ [Id.ident] + 1;
		NumberArray^ [Id.ident] := Leaf.number;
		IF Leaf.number > MaxNumberArray^ [Id.ident] THEN
		  MaxNumberArray^ [Id.ident] := Leaf.number;
		END;
		}


/*
 *		common functions
 */

FUNCTION	CodeExport		/ sections /


  Sections	(Export (codes), sections)
		{
		vExport := TRUE;
		CodeCodes (codes);
		CodeExport (sections);
		}

  Sections	(section, sections)
		CONDITION { section^.Kind # Tree.Export }
		{
		CodeExport (sections);
		}

  Sections0	() {}


FUNCTION	CodeGlobal		/ sections /


  Sections	(Global (codes), sections)
		{
		CodeCodes (codes);
		CodeGlobal (sections);
		}

  Sections	(section, sections)
		CONDITION { section^.Kind # Tree.Global }
		{
		CodeGlobal (sections);
		}

  Sections0	() {}


FUNCTION	CodeLocal		/ sections /


  Sections	(Local (codes), sections)
		{
		CodeCodes (codes);
		CodeLocal (sections);
		}

  Sections	(section, sections)
		CONDITION { section^.Kind # Tree.Local }
		{
		CodeLocal (sections);
		}

  Sections0	() {}


FUNCTION	CodeBegin		/ sections /


  Sections	(Begin (codes), sections)
		{
		CodeCodes (codes);
		CodeBegin (sections);
		}

  Sections	(section, sections)
		CONDITION { section^.Kind # Tree.Begin }
		{
		CodeBegin (sections);
		}

  Sections0	() {}


FUNCTION	CodeClose		/ sections /


  Sections	(Close (codes), sections)
		{
		CodeCodes (codes);
		CodeClose (sections);
		}

  Sections	(section, sections)
		CONDITION { section^.Kind # Tree.Close }
		{
		CodeClose (sections);
		}

  Sections0	() {}


FUNCTION	CodeCodes		/ codes /

  Codes		(Code (), codes)
		{
.. ^
		IF LINE THEN
.		   # line {n Code.pos.Line} "{s SourceFileName}"
		ELSE
.		   /* line {n Code.pos.Line} "{s SourceFileName}" */
		END;
		WriteString (file, Code.ref);
		CodeCodes2 (codes);
.
.. ^ Spaces;
		}

  Codes0	() {}


FUNCTION	CodeCodes2		/ codes /

  Codes		(Code (), codes)
		{
		WriteString (file, Code.ref);
		CodeCodes2 (codes);
		}

  Codes0	() {}

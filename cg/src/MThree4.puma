
(* file MThree4.puma 
   Modula-3 generation of attribute evaluator with optimized attribute storage. 
   Rodney M. Bates Oct 97 
   Extensively modified from file mod4.puma, which had these header comments:  
     /* Ich, Doktor Josef Grosch, Informatiker, 19.6.1990 */
*)

TRAFO EvalMThree2
TREE Tree
PUBLIC EvalModule

GLOBAL {

FROM SYSTEM	IMPORT ADR;
FROM IO		IMPORT WriteS, WriteNl, WriteI, StdOutput;
FROM StringMem	IMPORT WriteString;
FROM Idents	IMPORT tIdent;
FROM Texts	IMPORT WriteText;
FROM Sets	IMPORT tSet, MakeSet, ReleaseSet, Include, Minimum, IsElement, WriteSet;
FROM Relations	IMPORT IsRelated;
FROM TreeMThree2	IMPORT WriteLine;
FROM EvalMThree	IMPORT EvalModuleHead;

FROM Tree	IMPORT
   NoTree	, tTree		, Referenced	, NoCodeClass	,
   Computed	, Reverse	, Write		, Read		,
   Inherited	, Synthesized	, Input		, Output	,
   Stack	, Parameter	, Variable	,
   CopyDef	, CopyUse	, Thread	, Virtual	,
   Test		, Left		, Right		,
   NonBaseComp	, First		, Dummy		, Trace		,
   Demand	, Funct		, NoClass	, Options	,
   TreeRoot	, iModule	, iMain		, itTree	,
   ForallClassesExtensionsFirst, ForallAttributes, f		, WI	, WN	,
   IdentifyAttribute, GrammarClass, cOAG	, MaxVisit	,
   WriteInstance;

VAR
   Count, Start, Stop, Visit, ChildVisit, i, j, k	: SHORTCARD;
   Node, Attr, Class, AttrClass, Child, Child2, ChildsClass	: tTree;

PROCEDURE Representative (i: CARDINAL): CARDINAL;	(* Class	*)
   VAR
      s		: tSet;
      r		: CARDINAL;
      Stable	: BOOLEAN;
      j, k	: CARDINAL;
   BEGIN
      WITH Class^.Class DO
	 MakeSet (s, InstCount);
	 Include (s, i);
	 REPEAT
	    Stable := TRUE;
	    FOR j := 1 TO InstCount DO
	       IF IsElement (j, s) THEN
		  IF CopyDef IN Instance^[j].Properties THEN
		     k := Instance^[j].CopyArg;
		     IF (Parameter IN Instance^[k].Attribute^.Attribute.Properties) AND
			NOT IsElement (k, s) THEN
			Include (s, k);
			Stable := FALSE;
		     END;
		  END;
		  IF CopyUse IN Instance^[j].Properties THEN
		     FOR k := 1 TO InstCount DO
			IF (CopyDef IN Instance^[k].Properties) AND
			   (Parameter IN Instance^[k].Attribute^.Attribute.Properties) AND
			   (Instance^[k].CopyArg = j) AND
			   NOT IsElement (k, s) THEN
			   Include (s, k);
			   Stable := FALSE;
			END;
		     END;
		  END;
	       END;
	    END;
	 UNTIL Stable;
	 r := Minimum (s);
	 IF r <= AttrCount THEN
	    Stable := TRUE;
	    j := r + 1;
	    LOOP
	       IF j > AttrCount THEN EXIT; END;
	       IF IsElement (j, s) THEN Stable := FALSE; EXIT; END;
	       INC (j);
	    END;
	    IF NOT Stable THEN
	       j := 1;
	       LOOP
		  k := Instance^ [j].Order;
		  IF (k <= AttrCount) AND IsElement (k, s) THEN r := k; EXIT; END;
		  INC (j);
	       END;
	    END;
	 END;
	 ReleaseSet (s);
	 RETURN r;
      END;
   END Representative;

PROCEDURE GenAttribute (i: CARDINAL; repr: BOOLEAN);	(* Class = subtype, Node = current type, k	*)
   BEGIN
      IF repr THEN
	 WITH Class^.Class.Instance^[i] DO
	    IF (Parameter IN Attribute^.Attribute.Properties) AND
	       (({CopyDef, CopyUse} * Properties) # {}) THEN
	       i := Representative (i);
	    END;
	 END;
      END;
      WITH Class^.Class.Instance^[i] DO
	 IF Virtual IN Properties THEN RETURN; END;
	 IF Left IN Properties THEN					(* left		*)
	    WITH Attribute^.Attribute DO
	       IF Parameter IN Properties THEN
		  IF IdentifyAttribute (Node, Name) = NoTree THEN
		     WI (Class^.Class.Name); !yy! WI (Name);		(* local	*)
		  ELSE
		     !yy! WI (Name);					(* param	*)
		  END;
	       ELSIF Demand IN Properties THEN				(* demand	*)
		  IF Funct IN Properties THEN
		     AttrClass := GetClass (Class, Name);		(* function	*)
		     !y! WI (AttrClass^.Class.Name); !y! WI (Name); ! ( TNode )! 
		  ELSIF i # k THEN
		     WI (Class^.Class.Name); !yy! WI (Name);		(* local	*)
		  ELSE
		     !yy! WI (Name);					(* param	*)
		  END;
	       ELSE							(* tree		*)
		  !TNode . ! WI (Name);
	       END;
	    END;
	 ELSE								(* right	*)
	    WITH Attribute^.Attribute DO
	       IF Parameter IN Properties THEN				(* param	*)
		  WI (Class^.Class.Name); !y! WI (Selector^.Child.Name); !y! WI (Name);
	       ELSIF Demand IN Properties THEN				(* demand	*)
		  IF Funct IN Properties THEN				(* function	*)
		     AttrClass := GetClass (Selector^.Child.Class, Name);
		     !y! WI (AttrClass^.Class.Name); !y! WI (Name);
		     ! (TNode . ! WI (Selector^.Child.Name); ! )! 
		  ELSE
		     WI (Class^.Class.Name); !y! WI (Selector^.Child.Name); !y! WI (Name);
		  END;
	       ELSE							(* tree		*)
		  !TNode . ! WI (Selector^.Child.Name);
		  ! . ! WI (Name);
	       END;
	    END;
	 END;
      END;
   END GenAttribute;

PROCEDURE GetClass (Class: tTree; Attribute: tIdent): tTree;
   BEGIN
      WHILE Class^.Kind # NoClass DO
	 IF IdentifyAttribute (Class^.Class.Attributes, Attribute) # NoTree THEN RETURN Class; END;
	 Class := Class^.Class.BaseClass;
      END;
      RETURN NoTree;
   END GetClass;

PROCEDURE CheckUsage (Usage: BITSET): BOOLEAN;	(* Class, Child, Start, Stop	*)
   VAR i, i2: SHORTCARD;
   BEGIN
      FOR i := Start TO Stop DO
	 i2 := Class^.Class.Instance^ [i].Order;
	 WITH Class^.Class.Instance^ [i2] DO
	    IF ({Synthesized, Right, First} <= Properties) AND
	       (Child = Selector) AND
	       (Attribute^.Child.Partition <= Selector^.Child.Class^.Class.Visits) AND
	       (Attribute^.Child.Partition IN Usage) THEN
	       RETURN TRUE;
	    END;
	    IF ({Inherited, Right} <= Properties) AND (i2 = j) THEN
	       RETURN TRUE;
	    END;
	 END;
      END;
      RETURN FALSE;
   END CheckUsage;
}

BEGIN	{ k := 0; }

PROCEDURE EvalModule (t: Tree)
	
Ag (..) :- {
	EvalModuleHead (t);
      IF NOT IsElement (ORD ('9'), Options) THEN
	!!
	!PROCEDURE Eval ( yyt : ! WI (iMain); ! . ! WI (itTree); ! ) =!
	Node := Classes;
	Class := Classes;
	ForallAttributes (Class, GenTemposLocal);
	! BEGIN! 
	IF MaxVisit > 0 THEN
	   Class := Classes;
	   Visit := 1;
	   ! yyVisit1! WI (Class^.Class.Name); ! ( yyt ! 
	   ForallAttributes (Class, GenActualsLeft);
	   ! ) ;! 
	END;
	! END Eval;!
      ELSE
	!!
	!VAR xxStack: INTEGER ;!
	!!
	!PROCEDURE Eval ( yyt : ! WI (iMain); ! . ! WI (itTree); ! )!
	!=VAR xxHigh : BOOLEAN ;!
	Node := Classes;
	Class := Classes;
	ForallAttributes (Class, GenTemposLocal);
	! BEGIN !
	! xxStack := LAST (INTEGER);!
	IF MaxVisit > 0 THEN
	   Class := Classes;
	   Visit := 1;
	   ! yyVisit1 ! WI (Class^.Class.Name); ! ( yyt ! 
	   ForallAttributes (Class, GenActualsLeft);
	   ! ) ;! 
	END ; !!
	@  Wr . PutText ( Stdio . Stdout , "Stacksize " ) ;@
	!  Wr . PutText ( Stdio . Stdout!
        !               , Fmt . Int ( LOOPHOLE ( ADR ( xxHigh ) , INTEGER ) - xxStack ) ) ;!
	!  Wr . PutText ( Stdio . Stdout , Wr . EOL ) ;!
        ! END Eval ;!
      END;
	!!
	ForallClassesExtensionsFirst (Classes, GenDemandProc);
	ForallClassesExtensionsFirst (Classes, EvalModule);
	!PROCEDURE Begin! WI (EvalName); ! ( )!
	!=BEGIN!
	WriteLine (EvalCodes^.Codes.BeginLine);
	WriteText (f, EvalCodes^.Codes.Begin);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.BeginLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Begin);
	   Node := Node^.Module.Next;
	END;
	! END Begin! WI (EvalName); ! ;!
	!!
	!PROCEDURE Close! WI (EvalName); ! ( )!
	!=BEGIN!
	WriteLine (EvalCodes^.Codes.CloseLine);
	WriteText (f, EvalCodes^.Codes.Close);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.CloseLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Close);
	   Node := Node^.Module.Next;
	END;
	! END Close! WI (EvalName); ! ;!
	!!
	!BEGIN!
      IF IsElement (ORD ('X'), Options) THEN
	! yyf := Stdio . Stdout;!
      END;
	!END ! WI (EvalName); ! .!
}; .
Class (..) :- {
	IF (NoCodeClass * Properties) # {} THEN RETURN; END;
	IF Referenced IN Properties THEN
	   ForallClassesExtensionsFirst (Extensions, Generated0);
	   Generated := 0;
	   FOR Visit := 1 TO Visits DO
	      !PROCEDURE yyVisit! WN (Visit); WI (Name); ! ( yyt: ! WI (iMain); ! . ! WI (Name);
	      Node := t;
	      Class := t;
	      ForallAttributes (t, GenFormals);
	      ! ) =!
	      Node := t;
	      ForallClassesExtensionsFirst  ( Extensions , GenTempos ) ;
	      GenTempos (t);
	      WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	      WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	      Node := TreeRoot^.Ag.Modules;
	      WHILE Node^.Kind = Tree.Module DO
		 WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
		 WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
		 Node := Node^.Module.Next;
	      END;
      IF IsElement (ORD ('9'), Options) THEN
	      ! VAR xxLow : BOOLEAN ;!
	      ! BEGIN!
              !  xxStack := MIN ( xxStack , LOOPHOLE ( ADR ( xxLow ) , INTEGER ) ) ;!
(* FIX THIS^ This only works for stacks which grow toward zero. *)
      ELSE
	      ! BEGIN!
      END;
	      !   TYPECASE yyt OF!
	      IF cOAG IN GrammarClass THEN		(* generate evaluator	*)
		 Node := t;
		 ForallClassesExtensionsFirst ( Extensions , GenEvaluator ) ;
		 GenEvaluator (t);
	      END;
	      !   ELSE!
      IF IsElement (ORD ('Z'), Options) THEN
	      !    yyVisitParent ( yyt ) ;!
      END;
	      !   END;!
	      ! END yyVisit! WN (Visit); WI (Name); ! ;!
	      !!
	   END;
	END;
}; .

(* Not used.  Not converted to M3: 
PROCEDURE WriteType (t: Tree)

Class (..) :- {
	IF (NoCodeClass * Properties) # {} THEN RETURN; END;
	IF Trace IN Properties THEN
	   !| ! WI (iMain); ! . ! WI (Name); !: yyWriteS ('! WI (Name); !');!
	END;
}; .
*) 


PROCEDURE GenEvaluator	/* Node, Class (sometimes)	*/ (t: Tree)

Class (..) :- {
	IF (NoCodeClass * Properties) # {} THEN RETURN; END;
	IF (Generated = InstCount) THEN RETURN; END;
	!    | ! WI (iMain); ! . ! WI (Name); ! ( TNode ) =>!

	Start := Generated + 1;
	LOOP
	   IF Generated = InstCount THEN EXIT; END;
	   INC (Generated);
	   WITH Instance^ [Instance^ [Generated].Order] DO
	      IF (Left IN Properties) AND (Attribute^.Child.Partition > Visit) THEN
	         DEC (Generated); EXIT;
	      END;
	   END;
	END;
	Stop := Generated;

	Class := t;

	FOR Start := Start TO Stop DO
	   i := Instance^ [Start].Order;
	   WITH Instance^ [i] DO
	      IF ({Inherited, Right, First} <= Properties) AND ({Dummy, Virtual, Demand} * Properties = {}) THEN
		 GenDemandEval (t);
      IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
		 @      yyWriteEval ( TNode , "@ WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); @" ) ;@
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END; !!
		 IF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
		    !      write! WI (itTree); ! ( ! GenAttribute (i, TRUE); ! )!
		 ELSE
		    !      write! WI (Attribute^.Child.Type); ! ( ! GenAttribute (i, TRUE); ! ) yyWriteNl ;!
		 END;
      ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
		 @      yyWriteEval ( TNode , "@ WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); @" ) ;@
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      ELSE
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      END;
	      END;
	      IF ({Synthesized, Left, First} <= Properties) AND ({Dummy, Virtual, Demand} * Properties = {}) THEN
		 GenDemandEval (t);
      IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
		 @      yyWriteEval ( TNode , "@ WI (Attribute^.Child.Name); @" ) ;@
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END; !!
		 IF Test IN Properties THEN
		    !      writeBOOLEAN (yyb) yyWriteNl ;!
		 ELSIF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
		    !      write! WI (itTree); ! (! GenAttribute (i, TRUE); !)!
		 ELSE
		    !      write! WI (Attribute^.Child.Type); ! (! GenAttribute (i, TRUE); !) yyWriteNl;!
		 END;
      ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
		 @      yyWriteEval ( TNode , "@ WI (Attribute^.Child.Name); @" ) ;@
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      ELSE
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      END;
	      END;
	      IF ({Synthesized, Right, First} <= Properties) AND
		 (Attribute^.Child.Partition <= Selector^.Child.Class^.Class.Visits) THEN
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
		 @      yyWriteVisit ( TNode , "@ WI (Selector^.Child.Name); @ @ 
		 WN (Attribute^.Child.Partition); @" ) ;@
      END;
		 ChildVisit := Attribute^.Child.Partition;
		 Child := Selector;
		 !      yyVisit! WN (ChildVisit); WI (Child^.Child.Type);
		 ! ( TNode . ! WI (Child^.Child.Name);
		 ForallAttributes (Child^.Child.Class, GenActualsRight);
		 ! ) ;!
	      END;
	   END;
	END;
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	!yyVisitParent ( TNode ) ;!
      END;
}; .
Assign (..) :- {
	WriteLine (Pos);
	GenEvaluator (Results); ! := ! GenEvaluator (Arguments); ! ;!
}; .
Copy (..) :- {
	j := Class^.Class.Instance^[i].CopyArg;
	IF (Parameter IN Class^.Class.Instance^[i].Attribute^.Attribute.Properties) AND
	   (Parameter IN Class^.Class.Instance^[j].Attribute^.Attribute.Properties) THEN
	   IF (Left IN Class^.Class.Instance^[i].Properties) AND
	      (Left IN Class^.Class.Instance^[j].Properties) THEN

	      WriteLine (Pos);
	      GenAttribute (i, FALSE); ! := ! GenEvaluator (Arguments); ! ;!
	   END;
	ELSE
	   WriteLine (Pos);
	   GenEvaluator (Results); ! := ! GenEvaluator (Arguments); ! ;!
	END;
}; .
TargetCode (..) :- {
	WriteLine (Pos);
	GenEvaluator (Code); !!
}; .
Check (..) :- {
	WriteLine (Pos);
	IF Condition # NoTree THEN
      IF IsElement (ORD ('X'), Options) THEN
	   !      yyb := ! GenEvaluator (Condition); ! ; IF NOT yyb THEN ! 
      ELSE
	   !      IF NOT (! GenEvaluator (Condition); !) THEN ! 
      END;
	   GenEvaluator (Statement); !!
	   IF Actions^.Kind = Tree.Check THEN
	      !      ELSE ! GenEvaluator (Actions);
	   END;
	   !      END;!
	ELSE
      IF IsElement (ORD ('X'), Options) THEN
	   !      yyb := FALSE ; ! 
      END;
	   GenEvaluator (Statement); !;!
	   GenEvaluator (Actions);
	END;
}; .
Designator (..) :- {
	Child2 := IdentifyAttribute (Class, Selector);
	IF Child2 # NoTree THEN
	   ChildsClass := Child2^.Child.Class;
	   Attr := IdentifyAttribute (ChildsClass, Attribute);
	   IF Attr # NoTree THEN
	      GenAttribute (Class^.Class.AttrCount + Child2^.Child.InstOffset + Attr^.Attribute.AttrIndex, TRUE);
	   ELSE
	      WI (Selector); !:! WI (Attribute);
	   END;
	ELSE
	   WI (Selector); !:! WI (Attribute);
	END;
	GenEvaluator (Next);
}; .
Ident (..) :- {
	Attr := IdentifyAttribute (Class, Attribute);
	IF Attr # NoTree THEN
	   GenAttribute (Attr^.Attribute.AttrIndex, TRUE);
	ELSE
	   WI (Attribute);
	END;
	GenEvaluator (Next);
}; .
Any (..) :- {
	WriteString (f, Code);
	GenEvaluator (Next);
}; .
Anys (..) :- {
	GenEvaluator (Layouts);
	GenEvaluator (Next);
}; .
LayoutAny (..) :- {
	WriteString (f, Code);
	GenEvaluator (Next);
}; .


PROCEDURE Generated0 (t: Tree)

Class (..) :- {
	Generated := 0;
}; .


PROCEDURE GenFormals	/* Node, Class, Visit	*/ (t: Tree)

Attribute (..) :- {
	IF (Parameter IN Properties) AND (Visit IN Usage) THEN
	   !; VAR ! GenAttribute (AttrIndex, FALSE); ! : ! WI (Type);
	END;
}; .


PROCEDURE GenActualsRight	/* Node = current type, Class = subtype, Child, ChildVisit	*/ (t: Tree)

Attribute (..) :- {
	IF (Parameter IN Properties) AND (ChildVisit IN Usage) THEN
	   !, ! GenAttribute (Class^.Class.AttrCount + Child^.Child.InstOffset + AttrIndex, TRUE);
	END;
}; .


PROCEDURE GenActualsLeft	/* Node = current type, Class = subtype, Visit	*/ (t: Tree)

Attribute (..) :- {
	IF (Parameter IN Properties) AND (Visit IN Usage) THEN
	   !, ! GenAttribute (AttrIndex, FALSE);
	END;
}; .


PROCEDURE GenTempos	/* Node, Class (sometimes), Visit, k	*/ (t: Tree)

Class (..) :- {
	IF (NoCodeClass * Properties) # {} THEN RETURN; END;
	Start := Generated + 1;
	LOOP
	   IF Generated = InstCount THEN EXIT; END;
	   INC (Generated);
	   WITH Instance^ [Instance^ [Generated].Order] DO
	      IF (Left IN Properties) AND (Attribute^.Child.Partition > Visit) THEN
	         DEC (Generated); EXIT;
	      END;
	   END;
	END;
	Stop := Generated;
	Generated := Start - 1;

	Count := 0;
	Class := t;
	ForallAttributes (t, CountTempos);
	IF Count > 0 THEN
	   !  (* ! WI (iMain); ! . ! WI (Name); ! : *)!
	   Class := t;
	   ForallAttributes (t, GenTempos);
	END;
}; .
Child (..) :- {
	Child := t;
	ForallAttributes (Class, GenTemposChildren);
}; .
Attribute (..) :- {
	IF (Parameter IN Properties) AND (IdentifyAttribute (Node, Name) = NoTree) AND (Visit IN Usage) OR
	   (Demand IN Properties) AND NOT (Funct IN Properties) AND (AttrIndex # k) THEN
	   !  VAR ! GenAttribute (AttrIndex, FALSE); ! : ! WI (Type); ! ;!
	END;
}; .


PROCEDURE GenTemposChildren	/* Node, Class, Child	*/ (t: Tree)

Attribute (..) :- {
	IF (Parameter IN Properties) OR
	   (Demand IN Properties) AND NOT (Funct IN Properties) THEN
	   j := Class^.Class.AttrCount + Child^.Child.InstOffset + AttrIndex;
	   IF (Demand IN Properties) OR
	      (({CopyDef, CopyUse} * Class^.Class.Instance^[j].Properties) = {}) OR
	      (j = Representative (j)) THEN
	      IF CheckUsage (Usage) THEN
		 !  VAR ! GenAttribute (j, FALSE); ! : ! WI (Type); ! ;!
	      END;
	   END;
	END;
}; .


PROCEDURE CountTempos	/* Node, Class (sometimes), Visit, k	*/ (t: Tree)

Child (..) :- {
	Child := t;
	ForallAttributes (Class, CountTemposChildren);
}; .
Attribute (..) :- {
	IF (Parameter IN Properties) AND (IdentifyAttribute (Node, Name) = NoTree) AND (Visit IN Usage) OR
	   (Demand IN Properties) AND NOT (Funct IN Properties) AND (AttrIndex # k) THEN
	   INC (Count);
	END;
}; .


PROCEDURE CountTemposChildren	/* Node, Class, Child	*/ (t: Tree)

Attribute (..) :- {
	IF (Parameter IN Properties) OR
	   (Demand IN Properties) AND NOT (Funct IN Properties) THEN
	   j := Class^.Class.AttrCount + Child^.Child.InstOffset + AttrIndex;
	   IF (Demand IN Properties) OR
	      (({CopyDef, CopyUse} * Class^.Class.Instance^[j].Properties) = {}) OR
	      (j = Representative (j)) THEN
	      IF CheckUsage (Usage) THEN INC (Count); END;
	   END;
	END;
}; .


PROCEDURE GenTemposLocal	/* Node, Class	*/ (t: Tree)

Attribute (..) :- {
	IF Parameter IN Properties THEN
	   !  VAR ! GenAttribute (AttrIndex, FALSE); ! : ! WI (Type); ! ;!
	END;
}; .


PROCEDURE GenDemandProc	/* Node, Class	*/ (t: Tree)

Class (..) :- {
	IF (NoCodeClass * Properties) # {} THEN RETURN; END;
	Node := t;
	ForallAttributes (Attributes, GenDemandProc);
(* RMB 10-97 ^ I believe this needs to be done only for the immediate attributes and not the
               attributes of all base classes (ForAllAttributes will do the latter), because calls
               on demand procedures always find the exact class of the attribute and use it
               to build the demand procedure name. Calls are generated in GenDemandEval and
               GenAttribute. *) 
}; .
Attribute (..) :- {
	IF Demand IN Properties THEN
	   Class := Node;
	   k := AttrIndex;
	   IF Funct IN Properties THEN
	      !PROCEDURE y! WI (Class^.Class.Name); !y! WI (Name);
	      ! ( yyt : ! WI (iMain); ! . ! WI (Class^.Class.Name); ! ) : ! WI (Type); !=!
	   ELSE
	      !PROCEDURE y! WI (Class^.Class.Name); !y! WI (Name);
	      ! ( yyt : ! WI (iMain); ! . ! WI (Class^.Class.Name); 
              ! ; VAR ! GenAttribute (AttrIndex, FALSE); ! : ! WI (Type); ! ) =!
	   END;
	   ForallClassesExtensionsFirst ( Class^.Class.Extensions , GenTempos ) ;
	   GenTempos (Class);
	   !  BEGIN!
	   !    TYPECASE yyt OF!
	   IF cOAG IN GrammarClass THEN
	      i := AttrIndex;
	      Class := Node;
	      ForallClassesExtensionsFirst ( Class^.Class.Extensions , GenDemandProc2 ) ;
	      GenDemandProc2 (Class);
	      Class := Node;
	   END;
	   !    ELSE!
	   !    END;!
	   !  END y! WI (Class^.Class.Name); !y! WI (Name); !;!
	   !!
	   k := 0;
	END;
}; .

PROCEDURE GenDemandProc2	/* Node, Class, i	*/ (t: Tree)

Class (..) :- {
	!    | ! WI (iMain); ! . ! WI (Name); ! ( TNode ) =>!
	WITH Instance^ [i] DO
	   Class := t;
	   GenDemandEval (t);
	   IF Funct IN Properties THEN
	      GenDemandProc2 (Action);
	   ELSE
	      GenEvaluator (Action); !!
	   END;
	END;
}; .
Assign (..) :- {
	!      RETURN ! GenEvaluator (Arguments); !;!
}; .
Copy (..) :- {
	!      RETURN ! GenEvaluator (Arguments); !;!
}; .
TargetCode (..) :- {
	!NoBlockStatementForDemandFunctionAttributes;!
}; .


PROCEDURE GenDemandEval	/* Node, Class, i	*/ (t: Tree)

Class (..) :- {
	FOR j := 1 TO InstCount DO
	   WITH Instance^ [j] DO
	      IF IsRelated (i, j, DP) AND 
		 (Demand IN Properties) AND NOT (Funct IN Properties) THEN
		 IF Left IN Properties THEN
		    AttrClass := GetClass (t, Attribute^.Child.Name);
		    !      y! WI (AttrClass^.Class.Name); !y! WI (Attribute^.Child.Name);
		    ! ( TNode , ! GenAttribute (j, FALSE); ! ) ;!
		 ELSE
		    AttrClass := GetClass (Selector^.Child.Class, Attribute^.Child.Name);
		    !      y! WI (AttrClass^.Class.Name); !y! WI (Attribute^.Child.Name);
		    ! ( TNode . ! WI (Selector^.Child.Name);
		    ! , ! GenAttribute (j, FALSE); ! ) ;!
		 END;
	      END;
	   END;
	END;
}; .




(* file c.mod0 
   Rodney M. Bates Modifications: 
   9-97 Changed to use IOUtils.WriteBackslash 
  10-97 Removed unused import of TreeIO
*) 

TRAFO GramC
TREE Tree
PUBLIC ParsSpec ScanSpec

GLOBAL {

FROM IO		IMPORT WriteS, WriteNl;
FROM Strings	IMPORT tString, ArrayToString;
FROM StringMem	IMPORT WriteString;
FROM Idents	IMPORT NoIdent, tIdent, MakeIdent;
FROM Texts	IMPORT WriteText;
FROM Sets	IMPORT IsElement, Include;
FROM IOUtils    IMPORT WriteBackslash ; 

FROM Tree	IMPORT
   NoTree	, tTree		, Input		, Reverse	,
   Class	, NoClass	, Child		, Attribute	,
   ActionPart	, HasSelector	, HasAttributes	, NoCodeAttr	,
   Referenced	, Options	, TreeRoot	, QueryTree	,
   ClassCount	, iNoTree	, itTree	, Generated	,
   f		, WI, WE, WN	, ForallClasses	, ForallAttributes,
   Nonterminal	, Terminal	, IdentifyAttribute,
   String	, iPosition	;

IMPORT Strings;

VAR
   Node, ActClass, TheClass, TheAttr	: tTree;
   iOper, iLeft, iRight, iNone, iPrec, iRule	: tIdent;
   ActActionIndex, PrevActionIndex	: SHORTCARD;
   IsImplicit				: BOOLEAN;
   s					: tString;

PROCEDURE GetBaseClass (Class: tTree): tTree;
   BEGIN
      WHILE Class^.Class.BaseClass^.Kind # NoClass DO
	 Class := Class^.Class.BaseClass;
      END;
      RETURN Class;
   END GetBaseClass;

PROCEDURE IsLast (Class, Action: tTree): BOOLEAN;
   VAR Found, Last: BOOLEAN;
   BEGIN
      IsLast2 (Class, Action, Found, Last);
      RETURN Last;
   END IsLast;

PROCEDURE IsLast2 (t, Action: tTree; VAR pFound, pLast: BOOLEAN);
   VAR Found, Last: BOOLEAN;
   BEGIN
      CASE t^.Kind OF
      | Class:
	    IsLast2 (t^.Class.Attributes, Action, pFound, pLast);
	    IF pFound OR NOT pLast THEN RETURN; END;
	    IsLast2 (t^.Class.BaseClass, Action, pFound, pLast);
      | Child:
	    IsLast2 (t^.Child.Next, Action, Found, Last);
	    pFound := Found;
	    IF Found THEN
	       pLast := Last;
	    ELSE
	       pLast := FALSE;
	    END;
      | Attribute:
	    IsLast2 (t^.Attribute.Next, Action, pFound, pLast);
      | ActionPart:
	    IsLast2 (t^.ActionPart.Next, Action, Found, Last);
	    pFound := Found OR (Action = t);
	    IF Found THEN
	       pLast := Last;
	    ELSE
	       pLast := Last AND (Action = t);
	    END;
      ELSE
	    pFound := FALSE;
	    pLast  := TRUE;
      END;
   END IsLast2;

PROCEDURE Prefix;
   BEGIN
      IF TreeRoot^.Ag.ScannerName # NoIdent THEN WI (TreeRoot^.Ag.ScannerName); WriteS (f, "_"); END;
   END Prefix;
}

BEGIN {
   ArrayToString ("OPER"	, s); iOper	:= MakeIdent (s);
   ArrayToString ("RIGHT"	, s); iRight	:= MakeIdent (s);
   ArrayToString ("LEFT"	, s); iLeft	:= MakeIdent (s);
   ArrayToString ("NONE"	, s); iNone	:= MakeIdent (s);
   ArrayToString ("PREC"	, s); iPrec	:= MakeIdent (s);
   ArrayToString ("RULE"	, s); iRule	:= MakeIdent (s);
}

PROCEDURE ParsSpec (t: Tree)

Ag (..) :- {
	IF ScannerName # NoIdent THEN
	   WriteS (f, "SCANNER "); WI (ScannerName);
	END;
	WriteS (f, " PARSER "); WI (ParserName); WriteNl (f);
	WriteS (f, "GLOBAL {"); WriteNl (f);
	WriteText (f, ParserCodes^.Codes.Global);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteText (f, Node^.Module.ParserCodes^.Codes.Global);
	   Node := Node^.Module.Next;
	END;
        ParsVariant (Classes);
	WriteNl (f);
	WriteS (f, "typedef union {"); WriteNl (f);
	WriteS (f, " "); Prefix; WriteS (f, "tScanAttribute Scan;"); WriteNl (f);
	Node := Classes;
	WHILE Node^.Kind = Class DO
	  WITH Node^.Class DO
	     IF {Nonterminal, Referenced, HasAttributes} <= Properties THEN
	       IF (String IN Properties) AND NOT (HasSelector IN Properties) THEN
		 WriteS (f, " yy"); WN (Name); WriteS (f, " /* "); WE (Name); WriteS (f, " */ yy"); WN (Name); WriteS (f, ";"); WriteNl (f);
	       ELSE
		 WriteS (f, " yy"); WI (Selector); WriteS (f, " "); WI (Selector); WriteS (f, ";"); WriteNl (f);
	       END;
	     END;
	     Node := Next;
	  END;
	END;
	WriteS (f, "} tParsAttribute;"); WriteNl (f);
	WriteS (f, "}"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "EXPORT {"); WriteNl (f);
	WriteText (f, ParserCodes^.Codes.Export);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	  WriteText (f, Node^.Module.ParserCodes^.Codes.Export);
	  Node := Node^.Module.Next;
	END;
	WriteS (f, "}"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "LOCAL {"); WriteNl (f);
	WriteText (f, ParserCodes^.Codes.Local);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	  WriteText (f, Node^.Module.ParserCodes^.Codes.Local);
	  Node := Node^.Module.Next;
	END;
	WriteS (f, "}"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "BEGIN {"); WriteNl (f);
	WriteText (f, ParserCodes^.Codes.Begin);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	  WriteText (f, Node^.Module.ParserCodes^.Codes.Begin);
	  Node := Node^.Module.Next;
	END;
	WriteS (f, "}"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "CLOSE {"); WriteNl (f);
	WriteText (f, ParserCodes^.Codes.Close);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	  WriteText (f, Node^.Module.ParserCodes^.Codes.Close);
	  Node := Node^.Module.Next;
	END;
	WriteS (f, "}"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "TOKEN"); WriteNl (f);
	WriteNl (f);
	ForallClasses (Classes, Token);
	WriteNl (f);
	WriteS (f, "OPER"); WriteNl (f);
	WriteNl (f);
	PrecDefs (Precs);
	WriteNl (f);
	WriteS (f, "RULE"); WriteNl (f);
	WriteNl (f);
	ForallClasses (Classes, ParsSpec);
}; .
Class (..) :- {
	IF {Nonterminal, Referenced} <= Properties THEN
	   TheClass := t;
	   Grammar (t);
	END;
}; .


PROCEDURE ScanSpec (t: Tree)

Ag (..) :- {
	WriteS (f, "c"); WriteNl (f);
	WriteS (f, "# if defined __STDC__ | defined __cplusplus"); WriteNl (f);
	WriteS (f, "# define ARGS(parameters)	parameters"); WriteNl (f);
	WriteS (f, "# else"); WriteNl (f);
	WriteS (f, "# define ARGS(parameters)	()"); WriteNl (f);
	WriteS (f, "# endif"); WriteNl (f);
	WriteNl (f);
	ForallClasses (Classes, ScanVariant);
	WriteNl (f);
	WriteS (f, "typedef union {"); WriteNl (f);
	WriteS (f, " tPosition Position;"); WriteNl (f);
	ForallClasses (Classes, ScanAttr);
	WriteS (f, "} "); Prefix; WriteS (f, "tScanAttribute;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "extern void "); Prefix; WriteS (f, "ErrorAttribute ARGS((int Token, "); Prefix; WriteS (f, "tScanAttribute * pAttribute));"); WriteNl (f);
	WriteS (f, "%%"); WriteNl (f);
	WriteS (f, "void "); Prefix; WriteS (f, "ErrorAttribute"); WriteNl (f);
	WriteS (f, "# if defined __STDC__ | defined __cplusplus"); WriteNl (f);
	WriteS (f, " (int Token, "); Prefix; WriteS (f, "tScanAttribute * pAttribute)"); WriteNl (f);
	WriteS (f, "# else"); WriteNl (f);
	WriteS (f, " (Token, pAttribute) int Token; "); Prefix; WriteS (f, "tScanAttribute * pAttribute;"); WriteNl (f);
	WriteS (f, "# endif"); WriteNl (f);
	WriteS (f, "{"); WriteNl (f);
	WriteS (f, " pAttribute->Position = "); Prefix; WriteS (f, "Attribute.Position;"); WriteNl (f);
	WriteS (f, " switch (Token) {"); WriteNl (f);
	ForallClasses (Classes, ErrorActions);
	WriteS (f, " }"); WriteNl (f);
	WriteS (f, "}"); WriteNl (f);
	WriteS (f, "%%"); WriteNl (f);
	ForallClasses (Classes, ScanSpec);
}; .
Class (..) :- {
	IF {Terminal, Referenced} <= Properties THEN
	   WN (Code);
	   IF HasAttributes IN Properties THEN	WriteS (f, " S "); 
	   ELSE					WriteS (f, " N "); 
	   END;
	   IF (String IN Properties) AND NOT (HasSelector IN Properties) THEN
	      WriteS (f, "yy"); WN (Code);
	   ELSE
	      WI (Selector);
	   END;
	   WriteS (f, " "); WI (Name); WriteNl (f);
	END;
}; .


PROCEDURE ErrorActions (t: Tree)

Class (..) :- {
	IF {Terminal, Referenced, HasAttributes} <= Properties THEN
	  WriteS (f, " case /* "); WE (Name); WriteS (f, " */ "); WN (Code); WriteS (f, ": "); WriteNl (f);
	  TheClass := t;
	  ForallAttributes (t, ErrorActions);
	  WriteS (f, " break;"); WriteNl (f);
	END;
}; .
ActionPart (..) :- {
	ErrorActions (Actions);
}; .
Assign (..) :- {
	ErrorActions (Results); WriteS (f, "="); ErrorActions (Arguments); WriteS (f, ";"); WriteNl (f);
	ErrorActions (Next);
}; .
Copy (..) :- {
	ErrorActions (Results); WriteS (f, " = "); ErrorActions (Arguments); WriteS (f, ";"); WriteNl (f);
	ErrorActions (Next);
}; .
TargetCode (..) :- {
	ErrorActions (Code); WriteS (f, ";"); WriteNl (f);
	ErrorActions (Next);
}; .
Order (..) :- {
	ErrorActions (Next);
}; .
Check (..) :- {
	IF Statement # NoTree THEN
	   IF Condition # NoTree THEN
	      WriteS (f, "if ("); ErrorActions (Condition); WriteS (f, ") ; else { "); ErrorActions (Statement); WriteS (f, "; }"); WriteNl (f);
	   ELSE
	      WriteS (f, "{ "); ErrorActions (Statement); WriteS (f, "; }"); WriteNl (f);
	   END;
	ELSE
	   WriteS (f, "(void) ("); ErrorActions (Condition); WriteS (f, ");"); WriteNl (f);
	END;
	ErrorActions (Next);
}; .
Designator (..) :- {
	WI (Selector); WriteS (f, ":"); WI (Attribute);
	ErrorActions (Next);
}; .
Ident (..) :- {
	TheAttr := IdentifyAttribute (TheClass, Attribute);
	IF TheAttr # NoTree THEN
	   WriteS (f, "pAttribute->"); 
	   IF Attribute = iPosition THEN
           ELSIF (String IN TheClass^.Class.Properties) AND NOT (HasSelector IN TheClass^.Class.Properties) THEN
	      WriteS (f, "yy"); WN (TheClass^.Class.Code); WriteS (f, "."); 
	   ELSE
	      WI (TheClass^.Class.Selector); WriteS (f, "."); 
	   END;
	END;
	WI (Attribute);
	ErrorActions (Next);
}; .
Any (..) :- {
	WriteString (f, Code);
	ErrorActions (Next);
}; .
Anys (..) :- {
	ErrorActions (Layouts);
	ErrorActions (Next);
}; .
LayoutAny (..) :- {
	WriteString (f, Code);
	ErrorActions (Next);
}; .


PROCEDURE ScanVariant (t: Tree)

Class (..) :- {
	IF {Terminal, Referenced, HasAttributes} <= Properties THEN
	  WriteS (f, "typedef struct { tPosition yyPos; "); 
	  TheClass := t;
	  ForallAttributes (t, RecordField);
          IF (String IN Properties) AND NOT (HasSelector IN Properties) THEN
	    WriteS (f, "} /* "); WE (Name); WriteS (f, " */ yy"); WN (Code); WriteS (f, ";"); WriteNl (f);
	  ELSE
	    WriteS (f, "} yy"); WI (Selector); WriteS (f, ";"); WriteNl (f);
	  END;
	END;
}; .


PROCEDURE ScanAttr (t: Tree)

Class (..) :- {
	IF {Terminal, Referenced, HasAttributes} <= Properties THEN
          IF (String IN Properties) AND NOT (HasSelector IN Properties) THEN
	    WriteS (f, " yy"); WN (Code); WriteS (f, " /* "); WE (Name); WriteS (f, " */ yy"); WN (Code); WriteS (f, ";"); WriteNl (f);
	  ELSE
	    WriteS (f, " yy"); WI (Selector); WriteS (f, " "); WI (Selector); WriteS (f, ";"); WriteNl (f);
	  END;
	END;
}; .


PROCEDURE ParsVariant (t: Tree)

Class (..) :- {
	IF {Nonterminal, Referenced, HasAttributes} <= Properties THEN
	  WriteS (f, "typedef struct { "); 
	  TheClass := t;
	  ForallAttributes (Attributes, RecordField);
	  GenExt (Extensions);
          IF (String IN Properties) AND NOT (HasSelector IN Properties) THEN
	    WriteS (f, "} /* "); WE (Name); WriteS (f, " */ yy"); WN (Name); WriteS (f, ";"); WriteNl (f);
	  ELSE
	    WriteS (f, "} yy"); WI (Selector); WriteS (f, ";"); WriteNl (f);
	  END;
	END;
	ParsVariant (Next);
}; .


PROCEDURE GenExt (t: Tree)

Class (..) :- {
	ForallAttributes (Attributes, RecordField);
	GenExt (Extensions);
	GenExt (Next);
}; .


PROCEDURE Token (t: Tree)

Class (..) :- {
	IF {Terminal, Referenced} <= Properties THEN
	   WriteName (Name); WriteS (f, " = "); WN (Code); WriteNl (f);
	END;
}; .


PROCEDURE RecordField	/* TheClass	*/ (t: Tree)

Attribute (..) :- {
	IF (NoCodeAttr * Properties) = {} THEN 
	   IF (Nonterminal IN TheClass^.Class.Properties) OR (Name # iPosition) THEN
	      WI (Type); WriteS (f, " "); WI (Name); WriteS (f, "; "); 
	   END;
	END;
}; .


PROCEDURE PrecDefs (t: Tree)

LeftAssoc (..) :- {
	WriteS (f, "LEFT "); PrecDefs (Names); WriteNl (f);
	PrecDefs (Next);
}; .
RightAssoc (..) :- {
	WriteS (f, "RIGHT"); PrecDefs (Names); WriteNl (f);
	PrecDefs (Next);
}; .
NonAssoc (..) :- {
	WriteS (f, "NONE "); PrecDefs (Names); WriteNl (f);
	PrecDefs (Next);
}; .
Name (..) :- {
	WriteS (f, " "); WI (Name);
	PrecDefs (Next);
}; .


PROCEDURE Grammar (t: Tree)

Class (..) :- {
	IF Extensions^.Kind = Tree.NoClass THEN		(* Low ? *)
	   WITH TheClass^.Class DO
	      IF String IN Properties THEN WriteS (f, "yy"); WN (Name); ELSE WriteName (Name); END;
	   END;
	   WriteS (f, " : "); 
	   ActClass := t;
	   PrevActionIndex := 0;
	   IsImplicit := FALSE;
	   ForallAttributes (t, Rule);
	   IF Prec # NoIdent THEN WriteS (f, "PREC "); WI (Prec); WriteS (f, " "); END;
	   WriteS (f, "."); WriteNl (f);
	   PrevActionIndex := 0;
	   IsImplicit := TRUE;
	   ForallAttributes (t, Implicit);
	ELSE
	   Rule (Extensions);
	END;
}; .


PROCEDURE Rule (t: Tree)

Class (..) :- {
	Grammar (t);
	Rule (Next);
}; .
Child (..) :- {
	IF {String, Nonterminal} <= Class^.Class.Properties THEN WriteS (f, "yy"); WN (Type); ELSE WriteName (Type); END; WriteS (f, " "); 
}; .
ActionPart (..) :- {
	IF IsLast (ActClass, t) THEN
	   WriteS (f, "{"); 
	   IF PrevActionIndex # 0 THEN
	      Node := GetBaseClass (TheClass);
	      WITH Node^.Class DO
		 IF HasAttributes IN Properties THEN
		    WriteS (f, " $$."); 
		    IF String IN Properties THEN WriteS (f, "yy"); WN (Name); ELSE WI (Name); END;
		    WriteS (f, " = $"); WN (PrevActionIndex); WriteS (f, "."); 
		    IF String IN Properties THEN WriteS (f, "yy"); WN (Name); ELSE WI (Name); END;
		    WriteS (f, ";"); WriteNl (f);
		 END;
	      END;
	   END;
	   Rule (Actions);
	   WriteS (f, "} "); 
	ELSE
	   WriteS (f, "xx"); WN (Name); WriteS (f, " "); 
	END;
	PrevActionIndex := ParsIndex;
}; .
Assign (..) :- {
	Rule (Results); WriteS (f, "="); Rule (Arguments); WriteS (f, ";"); WriteNl (f);
	Rule (Next);
}; .
Copy (..) :- {
	Rule (Results); WriteS (f, " = "); Rule (Arguments); WriteS (f, ";"); WriteNl (f);
	Rule (Next);
}; .
TargetCode (..) :- {
	Rule (Code); WriteS (f, ";"); WriteNl (f);
	Rule (Next);
}; .
Order (..) :- {
	Rule (Next);
}; .
Check (..) :- {
	IF Statement # NoTree THEN
	   IF Condition # NoTree THEN
	      WriteS (f, "if ("); Rule (Condition); WriteS (f, ") ; else { "); Rule (Statement); WriteS (f, "; }"); WriteNl (f);
	   ELSE
	      WriteS (f, "{ "); Rule (Statement); WriteS (f, "; }"); 
	   END;
	ELSE
	   WriteS (f, "(void) ("); Rule (Condition); WriteS (f, ");"); WriteNl (f);
	END;
	Rule (Next);
}; .
Designator (..) :- {
	TheAttr := IdentifyAttribute (ActClass, Selector);
	IF TheAttr # NoTree THEN
	  Node := TheAttr^.Child.Class;
	  IF Node # NoTree THEN
	    WriteS (f, "$"); 
	    IF NOT IsImplicit THEN
	       WN (TheAttr^.Child.ParsIndex);
	    ELSE
	       WN (SHORTINT (TheAttr^.Child.ParsIndex + 1 - ActActionIndex));
	    END;
	    IF Nonterminal IN Node^.Class.Properties THEN	(* nonterminal *)
	      Node := GetBaseClass (Node);
	      IF (String IN Node^.Class.Properties) AND NOT (HasSelector IN Node^.Class.Properties) THEN
	        WriteS (f, ".yy"); WN (Node^.Class.Name);
	      ELSE
	        WriteS (f, "."); WI (Node^.Class.Name);
	      END;
	    ELSE						(* terminal *)
	      WriteS (f, ".Scan"); 
	      IF Attribute = iPosition THEN
	      ELSIF (String IN Node^.Class.Properties) AND NOT (HasSelector IN Node^.Class.Properties) THEN
	        WriteS (f, ".yy"); WN (Node^.Class.Code);
	      ELSE
	        WriteS (f, "."); WI (Node^.Class.Selector);
	      END;
	    END;
	    WriteS (f, "."); WI (Attribute);
	  ELSE
	    WI (Selector); WriteS (f, ":"); WI (Attribute);
	  END;
	ELSE
	  WI (Selector); WriteS (f, ":"); WI (Attribute);
	END;
	Rule (Next);
}; .
Ident (..) :- {
	TheAttr := IdentifyAttribute (ActClass, Attribute);
	Node := GetBaseClass (TheClass);
	IF TheAttr # NoTree THEN
	  IF (String IN Node^.Class.Properties) AND NOT (HasSelector IN Node^.Class.Properties) THEN
	    WriteS (f, "$$.yy"); WN (Node^.Class.Name); WriteS (f, "."); WI (Attribute);
	  ELSE
	    WriteS (f, "$$."); WI (Node^.Class.Name); WriteS (f, "."); WI (Attribute);
	  END;
	ELSE
	  WI (Attribute);
	END;
	Rule (Next);
}; .
Any (..) :- {
	WriteString (f, Code);
	Rule (Next);
}; .
Anys (..) :- {
	Rule (Layouts);
	Rule (Next);
}; .
LayoutAny (..) :- {
	WriteString (f, Code);
	Rule (Next);
}; .


PROCEDURE Implicit (t: Tree)

ActionPart (..) :- {
	IF NOT (Generated IN Properties) AND NOT IsLast (ActClass, t) THEN
	   INCL (Properties, Generated);
	   ActActionIndex := ParsIndex;
	   WriteS (f, "xx"); WN (Name); WriteS (f, " : {"); 
	   IF PrevActionIndex # 0 THEN
	      Node := GetBaseClass (TheClass);
	      WITH Node^.Class DO
		 IF HasAttributes IN Properties THEN
		    WriteS (f, " $$."); 
		    IF String IN Properties THEN WriteS (f, "yy"); WN (Name); ELSE WI (Name); END;
		    WriteS (f, " = $"); WN (SHORTINT (PrevActionIndex + 1 - ActActionIndex)); WriteS (f, "."); 
		    IF String IN Properties THEN WriteS (f, "yy"); WN (Name); ELSE WI (Name); END;
		    WriteS (f, ";"); WriteNl (f);
		 END;
	      END;
	   END;
	   Rule (Actions);
	   WriteS (f, "} ."); WriteNl (f);
	END;
	PrevActionIndex := ParsIndex;
}; .

PROCEDURE WriteName (Name: tIdent)

(iOper);
(iLeft);
(iRight);
(iNone);
(iPrec);
(iRule)	:-	    WriteBackslash ( f ) ; WI (Name); .
_	:-	    WI (Name); .

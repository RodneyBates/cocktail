
(* file MThree3.puma 
   Modula-3 generation of simple attribute evaluator. 
   Rodney M. Bates Oct 97 
   Extensively modified from file mod3.puma, which had these header comments:  
     /* Ich, Doktor Josef Grosch, Informatiker, 23.5.1989 */
   July 99 Add VAR to rule local decls. 
*)

TRAFO EvalMThree
TREE Tree
PUBLIC EvalInterface EvalModule EvalModuleHead GenEvaluator

EXPORT { VAR Class: Tree.tTree; }

GLOBAL {

FROM SYSTEM	IMPORT ADR;
FROM IO		IMPORT WriteS, WriteNl;
FROM StringMem	IMPORT WriteString;
FROM Texts	IMPORT WriteText;
FROM Sets	IMPORT IsElement;
FROM TreeMThree2	IMPORT WriteLine;
FROM EvalMThree3	IMPORT ToBit0;

FROM Tree	IMPORT
   NoTree	, tTree		, Child		, NoCodeClass	,
   Computed	, Reverse	, Write		, Read		,
   Inherited	, Synthesized	, Input		, Output	,
   CopyDef	, CopyUse	, Thread	, Virtual	,
   Test		, Left		, Right		,
   NonBaseComp	, First		, Dummy		, Trace		,
   Options	, TreeRoot	, iModule	, iMain		,
   itTree	, f		, WI	, WN	,
   IdentifyClass, IdentifyAttribute, GrammarClass, cOAG		,
   MaxVisit	;

VAR
   n		: SHORTCARD;
   Node		,
   Attr		,
   ChildsClass	: tTree;
}

PROCEDURE EvalInterface (t: Tree)
	
Ag (..) :- {
	WriteS (f, "INTERFACE "); WI (EvalName); WriteS (f, " ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "IMPORT "); WI (iMain); WriteS (f, " ;"); WriteNl (f);
      IF IsElement (ORD ('X'), Options) 
         OR IsElement (ORD ('Y'), Options) 
         OR IsElement (ORD ('Z'), Options) 
         OR IsElement (ORD ('L'), Options) 
         OR IsElement (ORD ('9'), Options) THEN 
        WriteS (f, "IMPORT Wr , Fmt ;"); WriteNl (f);
      END; 
      IF IsElement (ORD ('X'), Options) 
         OR IsElement (ORD ('Y'), Options) 
         OR IsElement (ORD ('Z'), Options) THEN 
        WriteS (f, "IMPORT Text ;"); WriteNl (f);
      END; 
        WriteS (f, "IMPORT Stdio , Text ;"); WriteNl (f);
	WriteLine (EvalCodes^.Codes.ImportLine);
	WriteText (f, EvalCodes^.Codes.Import);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.ImportLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Import);
	   Node := Node^.Module.Next;
	END;
	WriteLine (EvalCodes^.Codes.ExportLine);
	WriteText (f, EvalCodes^.Codes.Export);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.ExportLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Export);
	   Node := Node^.Module.Next;
	END;
	WriteNl (f);
	WriteS (f, "PROCEDURE Eval ( t : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " ) ;"); WriteNl (f);
	WriteS (f, "PROCEDURE Begin"); WI (EvalName); WriteS (f, " ( ) ;"); WriteNl (f);
	WriteS (f, "PROCEDURE Close"); WI (EvalName); WriteS (f, " ( ) ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "END "); WI (EvalName); WriteS (f, " ."); WriteNl (f);
}; .

PROCEDURE EvalModuleHead (t: Tree)
	
Ag (..) :- {
	WriteS (f, "# define DEP(a, b) a"); WriteNl (f);
	WriteS (f, "# define SELF TNode"); WriteNl (f);
	WriteS (f, "MODULE "); WI (EvalName); WriteS (f, ";"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "IMPORT "); WI (iMain); WriteS (f, ";"); WriteNl (f);
      IF IsElement (ORD ('Y'), Options) OR
         IsElement (ORD ('Z'), Options) THEN
      END;
      IF IsElement (ORD ('Y'), Options) OR
         IsElement (ORD ('Z'), Options) OR
         IsElement (ORD ('L'), Options) OR
         IsElement (ORD ('9'), Options) THEN
      END;
	WriteLine (EvalCodes^.Codes.GlobalLine);
	WriteText (f, EvalCodes^.Codes.Global);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.GlobalLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Global);
	   Node := Node^.Module.Next;
	END;
	WriteNl (f);
	WriteS (f, "VAR yyb	: BOOLEAN;"); WriteNl (f);
      IF IsElement (ORD ('X'), Options) THEN
	WriteS (f, '# include "yy'); WI (iModule); WriteS (f, '.w"'); WriteNl (f);
	WriteS (f, "# define yyWrite"); WI (iMain); WriteS (f, "(a) "); WI (iMain); WriteS (f, ".Write"); WI (iMain); WriteS (f, " (yyf, a)"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "VAR yyf	: Wr . T ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteHex ( VAR yyx : ARRAY OF CHAR ) "); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteS (f, "  IF yyTrace THEN"); WriteNl (f);
	WriteS (f, "   FOR yyi := 0 TO NUMBER ( yyx ) - 1 DO"); WriteNl (f);
	WriteS (f, "    Wr . PutText ( yyf , Fmt . Pad ( Fmt . Int ( ORD ( yyx [ yyi ] ) , 16 ) , 2 ) ) ;"); WriteNl (f);
	WriteS (f, "    Wr . PutChar ( yyf, ' ' ) ;"); WriteNl (f);
	WriteS (f, "   END ;"); WriteNl (f);
	WriteS (f, "  END;"); WriteNl (f);
	WriteS (f, " END yyWriteHex ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteNl ( ) "); WriteNl (f);
	WriteS (f, "=BEGIN IF yyTrace THEN"); WriteNl (f);
        WriteS (f, "  Wr . PutText ( Stdio . stdout , Wr . EOL ) ;"); WriteNl (f);
        WriteS (f, "  Wr . Flush ( Stdio . stdout ) ;"); WriteNl (f);
	WriteS (f, " END; END yyWriteNl;"); WriteNl (f);
      END;
      IF IsElement (ORD ('Y'), Options) OR
         IsElement (ORD ('Z'), Options) THEN
	WriteNl (f);
	WriteS (f, "CONST yyTrace = TRUE;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteS (yys: TEXT )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteS (f, "  Wr . PutText ( Stdio . Stdout , yys ) ;"); WriteNl (f);
        WriteS (f, '  Wr . PutText ( Stdio . Stdout , Fmt . Pad ( "" , MAX ( 0, 15 - Text . Length ( yys ) ) ) ) ;'); WriteNl (f);
	WriteS (f, " END yyWriteS;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteType ( yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteS (f, "  TYPECASE yyt OF"); WriteNl (f);
	WriteType ( Classes ) ;
	WriteS (f, "  ELSE"); WriteNl (f);
	WriteS (f, "  END;"); WriteNl (f);
	WriteS (f, " END yyWriteType;"); WriteNl (f);
      END;
      IF IsElement (ORD ('X'), Options) THEN
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteEval  (yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " ; yys : TEXT )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteS (f, "  IF yyTrace THEN"); WriteNl (f);
	WriteS (f, "   yyWriteType ( yyt ) ;"); WriteNl (f);
	WriteS (f, '   Wr . PutText ( Stdio . Stdout, " e " ) ;'); WriteNl (f);
	WriteS (f, "   Wr . PutText ( Stdio . Stdout, yys ) ;"); WriteNl (f);
	WriteS (f, '   Wr . PutText ( Stdio . Stdout , Fmt . Pad ( "" , MAX ( 0 , 23 - Text . Length ( yys ) ) ) ) ;'); WriteNl (f);
	WriteS (f, '   Wr . PutText ( Stdio . Stdout, " = " ) ;'); WriteNl (f);
	WriteS (f, "   Wr . Flush ( Stdio . Stdout) ;"); WriteNl (f);
	WriteS (f, "  END ;"); WriteNl (f);
	WriteS (f, " END yyWriteEval ;"); WriteNl (f);
      ELSIF IsElement (ORD ('Y'), Options) THEN
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteEval  (yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " ; yys : TEXT )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteS (f, "  IF yyTrace THEN"); WriteNl (f);
	WriteS (f, "   yyWriteType ( yyt ) ;"); WriteNl (f);
	WriteS (f, '   Wr . PutText ( Stdio . Stdout, " e " ) ;'); WriteNl (f);
	WriteS (f, "   Wr . PutText ( Stdio . Stdout, yys ) ;"); WriteNl (f);
	WriteS (f, "   Wr . PutText ( Stdio . Stdout , Wr . EOL ) ;"); WriteNl (f);
	WriteS (f, "   Wr . Flush ( Stdio . Stdout) ;"); WriteNl (f);
	WriteS (f, "  END ;"); WriteNl (f);
	WriteS (f, " END yyWriteEval;"); WriteNl (f);
      END;
      IF IsElement (ORD ('Z'), Options) THEN
	WriteNl (f);
	WriteS (f, "PROCEDURE yyWriteVisit ( yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " ; yys : TEXT )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteS (f, "  IF yyTrace THEN"); WriteNl (f);
	WriteS (f, "   yyWriteType ( yyt ) ;"); WriteNl (f);
	WriteS (f, '   Wr . PutText ( Stdio . Stdout, " v " ) ;'); WriteNl (f);
	WriteS (f, "   Wr . PutText ( Stdio . Stdout, yys ) ;"); WriteNl (f);
	WriteS (f, "   Wr . PutText ( Stdio . Stdout , Wr . EOL ) ;"); WriteNl (f);
	WriteS (f, "   Wr . Flush ( Stdio . Stdout) ;"); WriteNl (f);
	WriteS (f, "  END ;"); WriteNl (f);
	WriteS (f, " END yyWriteVisit ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE yyVisitParent ( yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " )"); WriteNl (f);
	WriteS (f, '=BEGIN yyWriteVisit ( yyt , "parent" ) ; END yyVisitParent ;'); WriteNl (f);
      END;
}; .

PROCEDURE EvalModule (t: Tree)
	
Ag (..) :- {
	EvalModuleHead (t);
      IF NOT IsElement (ORD ('9'), Options) THEN
	WriteNl (f);
	WriteS (f, "PROCEDURE Eval ( yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " )"); WriteNl (f);
	WriteS (f, "=BEGIN "); 
	IF MaxVisit > 0 THEN WriteS (f, "yyVisit1  (yyt ) ;"); END;
	WriteS (f, "END Eval ;"); WriteNl (f);
      ELSE
	WriteNl (f);
	WriteS (f, "VAR xxStack : INTEGER ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE Eval ( yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " )"); WriteNl (f);
	WriteS (f, "=VAR xxHigh : BOOLEAN ;"); WriteNl (f);
	WriteS (f, " BEGIN"); WriteNl (f);
	WriteS (f, "  xxStack := LAST ( INTEGER ) ;"); WriteNl (f);
	IF MaxVisit > 0 THEN
	WriteS (f, "  yyVisit1 ( yyt ) ;"); WriteNl (f);
	END;
	WriteS (f, '  Wr . PutText ( Stdio . Stdout , "Stacksize " ) ;'); WriteNl (f);
	WriteS (f, "  Wr . PutText ( Stdio . Stdout"); WriteNl (f);
        WriteS (f, "               , Fmt . Int ( LOOPHOLE ( ADR ( xxHigh ) , INTEGER ) - xxStack ) ) ;"); WriteNl (f);
	WriteS (f, "  Wr . PutText ( Stdio . Stdout , Wr . EOL );"); WriteNl (f);
	WriteS (f, " END Eval;"); WriteNl (f);
      END;
	WriteNl (f);
	FOR n := 1 TO MaxVisit DO
	   WriteS (f, "PROCEDURE yyVisit"); WN (n); WriteS (f, "  (yyt : "); WI (iMain); WriteS (f, " . "); WI (itTree); WriteS (f, " )"); WriteNl (f);
           WriteS (f, "="); WriteNl (f);
	   WriteLine (EvalCodes^.Codes.LocalLine);
	   WriteText (f, EvalCodes^.Codes.Local);
	   Node := Modules;
	   WHILE Node^.Kind = Tree.Module DO
	      WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
	      WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
	      Node := Node^.Module.Next;
	   END;
      IF IsElement (ORD ('9'), Options) THEN
	   WriteS (f, " VAR xxLow : BOOLEAN ;"); WriteNl (f);
	   WriteS (f, " BEGIN"); WriteNl (f);
	   WriteS (f, "  xxStack := MIN ( xxStack , LOOPHOLE ( ADR ( xxLow ) , INTEGER ) ) ;"); WriteNl (f);
(* FIX THIS^ This only works for stacks which grow toward zero. *)
      ELSE
	   WriteS (f, " BEGIN"); WriteNl (f);
      END;
	   WriteS (f, "  TYPECASE yyt OF"); WriteNl (f);
	   IF cOAG IN GrammarClass THEN		(* generate evaluator *)
	       GenEvaluator ( Classes ) ;
	   END;
	   WriteS (f, "  ELSE"); WriteNl (f);
      IF IsElement (ORD ('Z'), Options) THEN
	   WriteS (f, "   yyVisitParent ( yyt ) ;"); WriteNl (f);
      END;
	   WriteS (f, "  END;"); WriteNl (f);
	   WriteS (f, " END yyVisit"); WN (n); WriteS (f, " ;"); WriteNl (f);
	   WriteNl (f);
	END;
	WriteS (f, "PROCEDURE Begin"); WI (EvalName); WriteS (f, " ( )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteLine (EvalCodes^.Codes.BeginLine);
	WriteText (f, EvalCodes^.Codes.Begin);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.BeginLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Begin);
	   Node := Node^.Module.Next;
	END;
	WriteS (f, " END Begin"); WI (EvalName); WriteS (f, " ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "PROCEDURE Close"); WI (EvalName); WriteS (f, " ( )"); WriteNl (f);
	WriteS (f, "=BEGIN"); WriteNl (f);
	WriteLine (EvalCodes^.Codes.CloseLine);
	WriteText (f, EvalCodes^.Codes.Close);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.CloseLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Close);
	   Node := Node^.Module.Next;
	END;
	WriteS (f, " END Close"); WI (EvalName); WriteS (f, " ;"); WriteNl (f);
	WriteNl (f);
	WriteS (f, "BEGIN"); WriteNl (f);
      IF IsElement (ORD ('X'), Options) THEN
	WriteS (f, " yyf := Stdio . Stdout ;"); WriteNl (f);
      END;
	WriteS (f, "END "); WI (EvalName); WriteS (f, " ."); WriteNl (f);
}; .


PROCEDURE WriteType (t: Tree)

Class (..) :-
        WriteType ( Extensions ) ; 
	NoCodeClass * Properties = {{}};
	Trace IN Properties;
	WriteS (f, '  | '); WI (TreeRoot^.Ag.TreeName); WriteS (f, ' . t'); WI (Name); WriteS (f, ' => yyWriteS ( "'); WI (Name); WriteS (f, '" );'); WriteNl (f);
        WriteType ( Next ) ; 
	.

PROCEDURE GenEvaluator (t: Tree)

Class (..) :-
        GenEvaluator ( Extensions ) ; 
	NoCodeClass * Properties = {{}};
{	IF (Generated = InstCount) OR (Visits < n) THEN RETURN; END;
	WriteS (f, "  | "); WI (TreeRoot^.Ag.TreeName); WriteS (f, " . "); WI (Name); WriteS (f, " ( TNode ) =>"); WriteNl (f);
	Class := t;
	LOOP
	   IF Generated = InstCount THEN EXIT; END;
	   INC (Generated);
	   WITH Instance^ [Instance^ [Generated].Order] DO
	      IF (Left IN Properties) AND (Attribute^.Child.Partition > n) THEN
	         DEC (Generated); EXIT;
	      END;
	      IF ({Inherited, Right, First} <= Properties) AND NOT (Virtual IN Properties) THEN
      IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
		 WriteS (f, '    yyWriteEval ( TNode , "'); WI (Selector^.Child.Name); WriteS (f, ':'); WI (Attribute^.Child.Name); WriteS (f, '" );'); WriteNl (f);
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END; WriteNl (f);
		 IF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
		    WriteS (f, "    write"); WI (itTree);
		    WriteS (f, "( TNode . "); WI (Selector^.Child.Name); 
		    WriteS (f, " . "); WI (Attribute^.Child.Name); WriteS (f, ")"); WriteNl (f);
		 ELSE
		    WriteS (f, "    write"); WI (Attribute^.Child.Type);
		    WriteS (f, "( TNode . "); WI (Selector^.Child.Name);
		    WriteS (f, " . "); WI (Attribute^.Child.Name); WriteS (f, ")"); WriteNl (f);
                    WriteS (f, "yyWriteNl ;"); WriteNl (f);
		 END;
      ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
		 WriteS (f, '    yyWriteEval ( TNode , "'); WI (Selector^.Child.Name); WriteS (f, ':'); WI (Attribute^.Child.Name); WriteS (f, '" ) ;'); WriteNl (f);
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      ELSE
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      END;
	      END;
	      IF ({Synthesized, Left, First} <= Properties) AND ({Dummy, Virtual} * Properties = {}) THEN
      IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
		 WriteS (f, '    yyWriteEval ( TNode , "'); WI (Attribute^.Child.Name); WriteS (f, '" ) ;'); WriteNl (f);
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END; WriteNl (f);
		 IF Test IN Properties THEN
		    WriteS (f, "    writeBOOLEAN(yyb) yyWriteNl;"); WriteNl (f);
		 ELSIF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
		    WriteS (f, "    write"); WI (itTree);
		    WriteS (f, " ( TNode . "); WI (Attribute^.Child.Name); WriteS (f, ")"); 
		 ELSE
		    WriteS (f, "    write"); WI (Attribute^.Child.Type);
		    WriteS (f, " ( TNode . "); WI (Attribute^.Child.Name); WriteS (f, ") yyWriteNl;"); WriteNl (f);
		 END;
      ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
		 WriteS (f, '    yyWriteEval ( TNode , "'); WI (Attribute^.Child.Name); WriteS (f, '" ) ;'); WriteNl (f);
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      ELSE
		 IF Action # ADR (Action) THEN GenEvaluator (Action); END;
      END;
	      END;
	      IF ({Synthesized, Right, First} <= Properties) AND
		 (Attribute^.Child.Partition <= Selector^.Child.Class^.Class.Visits) THEN
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
		 WriteS (f, '    yyWriteVisit ( TNode , "'); WI (Selector^.Child.Name); WriteS (f, ' ');  
		 WN (Attribute^.Child.Partition); WriteS (f, '" ) ;'); WriteNl (f);
      END;
		 WriteS (f, "    yyVisit"); WN (Attribute^.Child.Partition);
		 WriteS (f, " ( TNode . "); WI (Selector^.Child.Name); WriteS (f, " ) ;"); WriteNl (f);
	      END;
	   END;
	END;
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	WriteS (f, "    yyVisitParent ( TNode ) ;"); WriteNl (f);
      END;
        GenEvaluator ( Next ) ; 
}; .
Assign (..) :- {
	WriteLine (Pos);
	GenEvaluator (Results); WriteS (f, " := "); GenEvaluator (Arguments); WriteS (f, " ;"); WriteNl (f);
}; .
Copy (..) :- {
	WriteLine (Pos);
	GenEvaluator (Results); WriteS (f, " := "); GenEvaluator (Arguments); WriteS (f, " ;"); WriteNl (f);
}; .
TargetCode (..) :- {
	WriteLine (Pos);
	GenEvaluator (Code); WriteNl (f);
}; .
Check (..) :- {
	WriteLine (Pos);
	IF Condition # NoTree THEN
      IF IsElement (ORD ('X'), Options) THEN
	   WriteS (f, "    yyb := "); GenEvaluator (Condition); WriteS (f, " ; IF NOT yyb THEN "); 
      ELSE
	   WriteS (f, "    IF NOT ( "); GenEvaluator (Condition); WriteS (f, " ) THEN "); 
      END;
	   GenEvaluator (Statement); WriteNl (f);
	   IF Actions^.Kind = Tree.Check THEN
	      WriteS (f, "    ELSE"); WriteNl (f);
	      GenEvaluator (Actions);
	   END;
	   WriteS (f, "    END ;"); WriteNl (f);
	ELSE
      IF IsElement (ORD ('X'), Options) THEN
	   WriteS (f, "    yyb := FALSE ; "); 
      END;
	   GenEvaluator (Statement); WriteS (f, ";"); WriteNl (f);
	   GenEvaluator (Actions);
	END;
}; .
Designator (..) :- {
	Attr := IdentifyAttribute (Class, Selector);
	IF Attr # NoTree THEN
	   ChildsClass := Attr^.Child.Class;
	   Attr := IdentifyAttribute (ChildsClass, Attribute);
	   IF NOT (Virtual IN Attr^.Attribute.Properties) THEN
	      WriteS (f, " TNode . "); WI (Selector); WriteS (f, " . "); WI (Attribute);
	   END;
	ELSE
	   WI (Selector); WriteS (f, ":"); WI (Attribute);
	END;
	GenEvaluator (Next);
}; .
Ident (..) :- {
	Attr := IdentifyAttribute (Class, Attribute);
	IF Attr # NoTree THEN
	   IF NOT (Virtual IN Attr^.Attribute.Properties) THEN
	      WriteS (f, " TNode . "); WI (Attribute);
	   END;
	ELSE
	   WI (Attribute);
	END;
	GenEvaluator (Next);
}; .
Remote (..) :-
   VAR TheClass : Class ; 
   VAR k : INTEGER ;
   TheClass := IdentifyClass (TreeRoot^.Ag.Classes, Type);
{  IF TheClass # NoTree THEN
      Attr := IdentifyAttribute (TheClass, Attribute);
      IF Attr # NoTree THEN
	 WITH Attr^.Attribute DO
	    k := ToBit0 (TheClass, AttrIndex);
	    IF Synthesized IN Properties THEN
	       WriteS (f, "REMOTE_SYN (yyIsComp, "); WN (k); WriteS (f, ", yyS"); WN (k); WriteS (f, ", "); 
	       GenEvaluator (Designators); WriteS (f, ", "); WI (t^.Remote.Type); WriteS (f, ", "); WI (Attribute); WriteS (f, ")"); 
	    ELSIF Inherited IN Properties THEN
	       WriteS (f, "REMOTE_INH (yyIsComp, "); WN (k); WriteS (f, ", "); WN (k); WriteS (f, ", "); 
	       GenEvaluator (Designators); WriteS (f, " , "); WI (t^.Remote.Type); WriteS (f, " , "); WI (Attribute); WriteS (f, " )"); 
	    ELSE
	       GenEvaluator (Designators); WriteS (f, " . "); WI (Attribute);
	    END;
	 END;
      END;
   END;
   GenEvaluator (Next);
}; .
Any (..) :- {
	WriteString (f, Code);
	GenEvaluator (Next);
}; .
Anys (..) :- {
	GenEvaluator (Layouts);
	GenEvaluator (Next);
}; .
LayoutAny (..) :- {
	WriteString (f, Code);
	GenEvaluator (Next);
}; .

